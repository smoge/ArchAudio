#Contributor: Damir Perisa <damir.perisa@bluewin.ch>
#Contributor: Robert Emil Berge <robert@rebi.no>

pkgname=ladspa-cmt-plugins
pkgver=1.16
pkgrel=2
pkgdesc="CMT for LADSPA"
arch=(i686 x86_64)
license=('GPL')
url="http://www.ladspa.org"
depends=('ladspa' 'gcc-libs')
groups=('ladspa-plugins')
conflicts=('cmt' 'ladspa-cmt')
provides=('cmt' 'ladspa-cmt')
source=(http://www.ladspa.org/download/cmt_src_${pkgver}.tgz)
md5sums=('15a875e5aaf79c209c223babfb41cb1c')

build() {

  cd "${srcdir}/cmt/src"

  sed -e "/^CFLAGS/ s/-O3/${CFLAGS}/" \
      -e 's|-Werror||g' \
      -i makefile 
  make 
}

package() {

  cd "${srcdir}/cmt/src"

  install -m755 -d "${pkgdir}/usr/lib/ladspa"
  install -m755 ../plugins/cmt.so "${pkgdir}/usr/lib/ladspa/" 
}
