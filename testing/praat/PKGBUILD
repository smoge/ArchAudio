# Contributor: jpate <j.k.pate@sms.ed.ac.uk>

pkgname=praat
pkgver=5.2.15
filever=5215
pkgrel=1
pkgdesc="a program for sound analysis and synthesis"
arch=('x86_64' 'i686')
url="http://www.fon.hum.uva.nl/praat/"
license=('GPL')
depends=( 'alsa-lib' 'gtk2' )
source=("http://www.fon.hum.uva.nl/praat/praat${filever}_sources.tar.gz")

build() {
  cd "$srcdir/sources_${filever}"

  cp makefiles/makefile.defs.linux.gtk makefile.defs

  sed -i 's|-I|-I/usr/include/gdk-pixbuf-2.0 -I|' makefile.defs
  sed -i 's|/usr/local/lib|/usr/lib|g' makefile.defs
  sed -i 's|/usr/local/include|/usr/include|g' makefile.defs

  make
}

package() {
  cd "$srcdir/sources_${filever}"

  install -Dm755  praat "$pkgdir/usr/bin/praat"
}

md5sums=('e66194ba9cf0c5ec5e6a29fdc3d6bb99')
