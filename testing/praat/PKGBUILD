# Contributor: jpate <j.k.pate@sms.ed.ac.uk>

pkgname=praat
pkgver=5.2.14
pkgrel=1
pkgdesc="a program for sound analysis and synthesis"
arch=('x86_64' 'i686')
url="http://www.fon.hum.uva.nl/praat/"
license=('GPL')
depends=( 'alsa-lib' 'gtk2' )
source=("http://www.fon.hum.uva.nl/praat/praat5214_sources.tar.gz")
md5sums=('fc821d26c05548ab94d951bf3294ae7f')

build() {
  cd "$srcdir/sources_5214/"

  cp makefiles/makefile.defs.linux.gtk makefile.defs

  sed -i 's|-I|-I/usr/include/gdk-pixbuf-2.0 -I|' makefile.defs
  sed -i 's|/usr/local/lib|/usr/lib|g' makefile.defs
  sed -i 's|/usr/local/include|/usr/include|g' makefile.defs

  make
}

package() {
  cd "$srcdir/sources_5214/"

  install -Dm755  praat "$pkgdir/usr/bin/praat"
}

