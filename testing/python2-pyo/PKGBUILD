# $Id$
# Maintainer: Arch Linux Pro Audio <dev@archaudio.org>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Bernardo Barros <bernardobarros@gmail.com>

pkgname=python2-pyo
pkgver=0.03
pkgrel=1
pkgdesc="A Python module written in C to help digital signal processing script creation"
arch=('i686' 'x86_64')
url="http://code.google.com/p/pyo/"
license=('GPL')
provides=('python-pyo' 'pyo')
conflicts=('python-pyo' 'pyo')
depends=('python2>=2.5' 'portmidi' 'portaudio' 'liblo' 'libsndfile')
source=("http://pyo.googlecode.com/files/pyo_$pkgver-src.tar.bz2"
  "http://pyo.googlecode.com/files/pyo_$pkgver-doc.tar.bz2")
md5sums=('8713cc383a84e0d7c104a291d1b94c0e'
         '83930f1d8c517a140b273cac6a00c2d1')

build() {

  cd "$srcdir/pyo_$pkgver-src"
  export PYTHON="python2"
  python2 setup.py --use-jack build
}

package() {

  cd "$srcdir/pyo_$pkgver-src"
  python2 setup.py install --use-jack --root="$pkgdir/"

  cd "$srcdir/pyo_$pkgver-doc"
  install -d $pkgdir/usr/share/doc/pyo
  cp -R * $pkgdir/usr/share/doc/pyo
}
