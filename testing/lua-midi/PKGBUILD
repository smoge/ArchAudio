# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkg=MIDI
pkgname=lua-midi
pkgver=5.5
pkgrel=1
pkgdesc="Lua module for reading, writing and manipulating MIDI data"
arch=(any)
url="http://www.pjb.com.au/comp/lua/MIDI.html"
license=('GPL')
depends=('lua' 'luaposix')
source=("http://www.pjb.com.au/comp/lua/$_pkg-$pkgver.tar.gz"
        "http://lua-users.org/files/wiki_insecure/dumper.lua")
md5sums=('9dc2628f5302ed9be72fab88e7677b86'
         'f5d3e62f6ba957e7ff496f762c479887')

build() {
  cd "$srcdir/$_pkg-$pkgver"

  # fix test_mi.lua DataDumper require
  sed -i "s|\(require[^D]*\)\(DataDumper\)|\1MIDI.dumper|" test/test_mi.lua
}

package() {
  cd "$srcdir/$_pkg-$pkgver"

  # library
  install -Dm644 $_pkg.lua "$pkgdir/usr/share/lua/5.1/$_pkg.lua"

  # additional datadumper library for test
  install -Dm644 ../dumper.lua "$pkgdir/usr/share/lua/5.1/MIDI/dumper.lua"

  # docs and examples
  install -Dm644 doc/$_pkg.html "$pkgdir/usr/share/doc/$pkgname/$_pkg.html"
  install -Dm644 test/test_mi.lua "$pkgdir/usr/share/$pkgname/examples/test_mi.lua"
}

# vim:set ts=2 sw=2 et:
