# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=din
pkgver=1.5
pkgrel=1
pkgdesc="A software musical instrument and audio synthesizer that let you use multiple Bezier curves to draw and sculpt waveforms"
arch=(i686 x86_64)
url="http://dinisnoise.org/"
license=('GPL')
depends=('liblo' 'libgl' 'libircclient' 'tcl' 'jack' 'sdl' 'fftw')
source=("http://din.googlecode.com/files/$pkgname-$pkgver.tar.gz")
md5sums=('2e157987370d69b07ff447cb0bb92744')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Fix share dir
  sed -i "s|var/|share/|" data/checkdotdin

  # Fix tcl include dir
  sed -i "s|tcl8.5/||g" configure*

  # Fix stdlib inclusion in bot.cc
  sed -i "40i#include <stdlib.h>" src/bot.cc

  ./configure --prefix=/usr \
              --localstatedir=/usr/share

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
