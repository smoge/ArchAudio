# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=qmidiarp
pkgver=0.3.9
pkgrel=1
pkgdesc="A MIDI arpeggiator, phrase generator and controller LFO for the ALSA sequencer."
arch=(i686 x86_64)
url="http://sourceforge.net/projects/qmidiarp/"
license=('GPL')
depends=('qt' 'jack')
conflicts=("$pkgname-cvs")
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.tar.bz2"
        "http://a.fsdn.com/con/icons/qm/qmidiarp@sf.net/qmidiarp2.png"
        "$pkgname.desktop")
md5sums=('8d857eea09cb7921d83df0745e5137e4'
         'ecc3ead7a8cec2f57735ee4ecc4a45dc'
         '404e45fcb7dbe5d97b32fbfa295db2a8')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # Install desktop && pixmaps
  install -Dm 644 ../qmidiarp2.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm 644 ../$pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
}
