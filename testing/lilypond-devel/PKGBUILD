# Maintainer: Patrick McCarty <pnorcks at gmail dot com>

pkgname=lilypond-devel
pkgver=2.13.61
pkgrel=2
pkgdesc="An automated music engraving system (development version)"
arch=('i686' 'x86_64')
url="http://lilypond.org/"
license=('GPL')
depends=('fontconfig' 'freetype2' 'ghostscript'
         'guile' 'pango' 'python2')
makedepends=('bison' 'flex' 'fontforge' 'gettext' 'gsfonts'
             'perl' 'texinfo' 't1utils' 'texlive-core')
optdepends=('netpbm: building HTML documentation'
            'imagemagick: building HTML documentation'
            'ttf-kochi-substitute: building HTML documentation'
            'texi2html>=1.82: building HTML documentation'
            'rsync: installing HTML documentation')
provides=("lilypond=$pkgver")
conflicts=('lilypond' 'lilypond-git')
install=$pkgname.install
source=(http://download.linuxaudio.org/lilypond/sources/v2.13/lilypond-$pkgver.tar.gz
        python2.patch
        texlive-workaround.patch)
sha256sums=('6aaf6bdd552eed636edb7c60d3699bf9f3ea7d479b2953f6c604d28abc5c69d5'
            '80f5be4313d08bd481d0dd04e767ebb79d1b99b05fb373b5d4cb96cdbb5cf4b4'
            'b0ec3b9b2e6f9a4ed7d4a40f8b0d86de565e37d513d6a6e02c556b59931124af')

build() {
  cd "$srcdir/lilypond-$pkgver"

  # python2 fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  patch -p1 -i "$srcdir/python2.patch"
  patch -p1 -i "$srcdir/texlive-workaround.patch"

  PYTHON="python2" ./autogen.sh --prefix=/usr
  make all
}

package() {
  cd "$srcdir/lilypond-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
