# Maintainer: Bernardo Barros <bernardobarros@gmail.com>

# guido doc: 
#   http://guidolib.sourceforge.net/doc/GUIDO-Music%20Notation%20Format.html

pkgname=guido
pkgver=1.40
pkgrel=1
pkgdesc="A formal language for score level music representation. It is a plain-text, i.e. readable and platform independent format capable of representing all information contained in conventional musical scores."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/guidolib"
license=('GPL')
optdepends=('qt' 'pd')
source=(http://sourceforge.net/projects/guidolib/files/Guidolib-src/guidolib-$pkgver-src.tgz)
md5sums=('8b89ad8f0d88d96844fefef682125420') 

build() {

  cd "$srcdir/guidolib-$pkgver-src"
  rm -fr build
  mkdir build
  cd build
  cmake ../cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
  make

}

package() {

  # There is no option to set lib folder to /usr/lib
  # For now I will install in usr/lib64 and then move manually
  # As soon as the devs make a CMake option, we can change this

  mkdir -p $pkgdir/usr/{include,lib64,lib}
  mkdir -p $pkgdir/usr/share/doc/guidolib/

  cd $srcdir/guidolib-$pkgver-src/build

  make DESTDIR="$pkgdir/" install

  mv "$pkgdir"/usr/lib64/* "$pkgdir"/usr/lib/
  rm -rf "$pkgdir"/usr/lib64
  
  # docs: 
  cp -rf $srcdir/guidolib-$pkgver-src/doc/ $pkgdir/usr/share/doc/guidolib/
}
