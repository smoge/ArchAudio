# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=rtaudio
pkgver=4.0.7
pkgrel=1
pkgdesc="A set of C++ classes that provide a common API for realtime audio input/output."
arch=(i686 x86_64)
url="http://www.music.mcgill.ca/~gary/rtaudio/"
license=('GPL')
depends=('jack')
source=("${url}release/${pkgname}-${pkgver}.tar.gz")
md5sums=('5d1292abccffa37505d9c4cc177b1e4f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export CPPFLAGS="$CPPFLAGS -I/usr/include/$pkgname"

  ./configure --prefix=/usr \
              --with-alsa \
              --with-oss \
              --with-jack
  make
  cd tests && make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # Shared lib
  install -Dm755 librtaudio.so "$pkgdir/usr/lib/librtaudio.so"

  # Configuration utility
  install -Dm755 rtaudio-config "$pkgdir/usr/bin/rtaudio-config"

  # Docs
  install -d "$pkgdir"/usr/{include,share/doc}/$pkgname
  cp -a doc/{html,images,release.txt} "$pkgdir/usr/share/doc/$pkgname"

  # Headers
  install *.h include/* "$pkgdir/usr/include/$pkgname"

  # Utilities
  for _ex in `find tests -maxdepth 1 -perm 755 -type f`; do
    install -Dm755 $_ex "$pkgdir/usr/bin/$pkgname-${_ex/*\//}"
  done
}

# vim:set ts=2 sw=2 et:
