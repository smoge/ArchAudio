# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Bernardo Barros <bernardobarros@gmail.com>

_p=muse
pkgname=${_p}2
pkgver=2.0beta2
pkgrel=1
pkgdesc="A MIDI/Audio sequencer with recording and editing capabilities"
arch=('i686' 'x86_64')
url="http://muse-sequencer.org/"
license=('GPL')
depends=('qt' 'fluidsynth' 'dssi' 'lash')
makedepends=('cmake' 'doxygen' 'graphviz')
install="$pkgname.install"
source=("http://switch.dl.sourceforge.net/lmuse/$_p-$pkgver.tar.gz")
md5sums=('1da3b10bcde157b1fee5921267ada8ac')

build() {
  # Does not link with as-needed
  export LDFLAGS="${LDFLAGS//-Wl,--as-needed}"
 
  cd "$srcdir/$_p-$pkgver"

  # Python2 Fixes
  sed -i "s|\(env python\).*|\12|;s|\(bin/python\).*|\12|" \
    `grep -Erl "(env python|bin/python)" .`

  # Create build dir
  [[ -d build ]] || mkdir build && cd build

  cmake -DCMAKE_BUILD_TYPE=release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DENABLE_FLUID=1 \
        -DENABLE_DSSI=1 \
        -DENABLE_LASH=1 \
        -DENABLE_OSC=1 ..
  make
}

package() {
  cd "$srcdir/$_p-$pkgver/build"

  make DESTDIR="$pkgdir" install

  # Prevent conflicts with Muse qt3 ..
  cd "$pkgdir/usr/share/applications"
  mv $_p.desktop $pkgname.desktop
  sed -i "s|sE|& 2|" $pkgname.desktop

  # .. and oomidi
  # grepmidi bin
  cd "$pkgdir/usr/bin"
  mv grepmidi $pkgname-grepmidi

  # grepmidi man
  cd "$pkgdir/usr/share/man/man1"
  mv grepmidi.1 $pkgname-grepmidi.1
}
