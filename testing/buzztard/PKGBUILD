# Maintainer: SpepS <dreamspepser at yahoo dot it>
# Contributor: Angelo Theodorou <encelo@users.sourceforge.net>

pkgname=buzztard
pkgver=0.5.0
pkgrel=2
pkgdesc="A modular, free, open source music studio that is conceptually based on Buzz"
arch=('i686' 'x86_64')
url="http://www.buzztard.org"
license=('GPL')
depends=('gnome-icon-theme' 'gnome-vfs' 'gst-buzztard' 'hicolor-icon-theme'
         'libgsf' 'libgnomecanvas' 'gstreamer0.10-base' 'shared-mime-info')
options=('!libtool')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/sourceforge/buzztard/$pkgname-$pkgver.tar.gz"
	"configure.ac")
md5sums=('f6aac9aa1c4d4c5c4ae342fc37c6001e'
         'aff48e9712522bae191db3d0e3892645')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # fails with --as-needed
  export LDFLAGS="${LDFLAGS//-Wl,--as-needed}"

  # genarating a configure script
  # with the introspection option
  cp ../configure.ac .
  autoconf

  ./configure --prefix=/usr \
              --disable-introspection \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # Deleting unneeded files
  rm -f "$pkgdir/usr/share/applications/mimeinfo.cache"
  find "$pkgdir/usr/share/mime" -maxdepth 1 -type f -exec rm {} \;

  # Desktop file icon fix
  sed -i "s|/.*s/||" "$pkgdir/usr/share/applications/buzztard-edit.desktop"
}
