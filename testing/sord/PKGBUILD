# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=sord
pkgver=0.4.0
pkgrel=1
pkgdesc="A lightweight C library for storing RDF data in memory."
arch=(i686 x86_64)
url="http://drobilla.net/software/$pkgname/"
license=('custom:ISC')
depends=('serd' 'glib2')
makedepends=('python2')
conflicts=("$pkgname-svn")
source=("http://download.drobilla.net/$pkgname-$pkgver.tar.bz2")
md5sums=('2076abf311af14c1dbbc41337cfcba23')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # remove ldconfig
  sed -i "/ldconfig/d" wscript

  # add LIBPATH_SERD env to wscript
  sed -i "/LIBPATH_SORD/p;s/LIBPATH_SORD/LIBPATH_SERD/" wscript

  python2 ./waf configure --prefix=/usr \
                          --mandir=/usr/share/man
  python2 ./waf
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  DESTDIR="$pkgdir" python2 ./waf install

  # license
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
