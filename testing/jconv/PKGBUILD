# Maintainer: Arch Linux Pro Audio <dev@archaudio.org>
# Contributor: Philipp Ãœberbacher <hollunder at gmx dot at>
pkgname=jconv
pkgver=0.8.1
pkgrel=1
pkgdesc="Convolution Engine for JACK, including example config and reverbs files."
url="http://www.kokkinizita.net/linuxaudio/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('clthreads' 'zita-convolver' 'jack-audio-connection-kit')
install=jconv.install
source=(http://www.kokkinizita.net/linuxaudio/downloads/${pkgname}-${pkgver}.tar.bz2 \
http://www.kokkinizita.net/linuxaudio/downloads/jconv-reverbs.tar.bz2 \
jconv.install)
md5sums=('7c194e36ecc59fd443ad08405cb40d73'
         'a33ec6a97fac039400f7674f3bde4ca9'
         '6ca78a34a9d1b53c32b823e7ae3b3a5e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's|/usr/bin/install|install|' Makefile || return 1
  sed -i 's|usr\/local|usr|' Makefile || return 1

  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -d "${pkgdir}/usr/bin/" || return 1

  #this installs the configuration readme
  install -Dm 644 README.CONFIG "${pkgdir}/usr/share/jconv/README.CONFIG" || return 1

  #this installs the example config files and reverbs that come with jconv
  cp -r config-files/ "${pkgdir}/usr/share/jconv/" || return 1

  #this installs additional reverbs
  cp -r ${srcdir}/reverbs/* "${pkgdir}/usr/share/jconv/config-files/" || return 1

  make PREFIX="${pkgdir}/usr" install || return 1
}
