# $Id$
# Maintainer: Arch Linux Pro Audio <dev@archaudio.org>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Flemming Madsen (arch at themadsens dot dk)

pkgname=spiralsynthmodular
pkgver=0.2.2a
pkgrel=2
arch=('i686' 'x86_64')
pkgdesc="An object orientated music studio with an emphasis on live use. Comes with lots of native plugins and support for ladspa"
url="http://www.pawfal.org/Software/SSM"
license="GPL"
depends=('fltk' 'liblrdf' 'jack')
makedepends=('ladspa')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/spiralmodular/spiralmodular/0.2.2a%20%28Latest%20Version%29/spiralmodular-${pkgver}.tar.bz2")
md5sums=('e4f44908f72447995103009a2bbf3d56')

build() {
  cd "$srcdir/spiralmodular-0.2.2"

  # Compatibility Fixes
  sed -e "27i#include <string.h>" \
      -e "s|Sample::||g" \
      -i SpiralSound/Sample.h

  sed -e "21i#include <string.h>" \
      -e "22i#include <stdlib.h>" \
      -i SpiralSound/ChannelHandler.C

  sed -i "s|static char|static const char|g" `grep -rl "static char" .`
  sed -i "s|^ *char\*\*|const &|" `grep -rl "char\*\*" .`

  sed -i "s|JackPort::||" \
    SpiralSound/Plugins/JackPlugin/JackPlugin.h

  sed -i "s|CVGUI::||" \
    SpiralSound/Plugins/ControllerPlugin/ControllerPluginGUI.h

  sed "23i#include <stdlib.h>" \
    -i SpiralSound/Plugins/OscillatorPlugin/OscillatorPlugin.C \
    -i SpiralSound/Plugins/NoisePlugin/NoisePlugin.C \
    -i SpiralSound/Plugins/MasherPlugin/MasherPlugin.C \
    -i SpiralSound/Plugins/CounterPlugin/CounterPluginGUI.C \
    -i SpiralSound/Plugins/ControllerPlugin/ControllerPluginGUI.C

  # local path strip
  sed -i "s|/local||g" configure

  ./configure --prefix=/usr \
              --with-plugindir=/usr/lib/SpiralPlugins
  make
}

package() {
  cd "$srcdir/spiralmodular-0.2.2"

  install -d "$pkgdir"/usr/{bin,share/SpiralSynth}

  make prefix="$pkgdir/usr" install

  # Install Examples
  cp -a Examples "$pkgdir/usr/share/SpiralSynth"
}
