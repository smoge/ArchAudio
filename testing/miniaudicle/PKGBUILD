# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkg=miniAudicle
pkgname=${_pkg/A/a}
pkgver=0.2.0
pkgrel=3
pkgdesc="A light-weight integrated development environment for the ChucK digital audio programming language."
arch=('i686' 'x86_64')
url="http://audicle.cs.princeton.edu/mini/"
license=('GPL')
[ $CARCH = 'x86_64' ] && \
depends=('lib32-alsa-lib' 'lib32-libsndfile' 'lib32-wxgtk') && \
makedepends=('gcc-multilib') && \
conflicts=('bin32-miniaudicle') || \
depends=('libsndfile' 'wxgtk')
source=("http://$pkgname.googlecode.com/files/$_pkg-$pkgver.tgz")
md5sums=('37389af75bd98de127c71e08d47d43c7')

build() {
  cd "$srcdir/$_pkg-$pkgver"

  # x86_64 multilib build configuration
  if [ $CARCH = 'x86_64' ]; then
    unset CFLAGS
    _WXCONFIG="/usr/lib32/wx/config/gtk2-unicode-release-2.8"

    sed "s|?||g;s|g[c+][c+]|& -m32|;s|wx-config|$_WXCONFIG|g" \
        -i makefile.alsa \
        -i chuck/src/makefile.alsa
  fi

  make -f makefile.alsa
}

package() {
  cd "$srcdir/$_pkg-$pkgver/wxw"

  install -Dm755 miniAudicle "$pkgdir/usr/bin/miniaudicle"
}