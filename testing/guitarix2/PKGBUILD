# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkg=guitarix
pkgname=${_pkg}2
pkgver=0.16.0
pkgrel=2
pkgdesc="A simple mono amplifier to be used in a 'JACKified' environment."
arch=('i686' 'x86_64')
url="http://sourceforge.net/apps/wordpress/guitarix/"
license=('GPL')
depends=('gtkmm' 'jack')
makedepends=('python2' 'boost')
conflicts=("$_pkg" 'gx_head')
replaces=("$_pkg" 'gx_head')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/$_pkg/$_pkg/$pkgname-$pkgver.tar.bz2")
md5sums=('454d71381fbdc42a2f4da63b67333c79')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fixes
  sed -i "s|\(^#!.*bin/python\).*|\12|;s|\(^#!.*env python\).*|\12|;" \
    `grep -Erl "(env python|bin/python)" .`

  # python2 fixes for python wrapper (do not build actually)
  sed -i "s|\(\"python\) |\12 |g" {pygxw,libgxwmm/gxwmm}/wscript

  # remove ldconfig in wscript
  sed -i "579,580d" wscript

  python2 waf configure --prefix=/usr \
                        --shared-lib \
                        --glade-support \
                        --includeresampler \
                        --includeconvolver
#                        --python-wrapper \
#                        --generate-cpp-wrapper \
  python2 waf build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf install --destdir="$pkgdir"
}
