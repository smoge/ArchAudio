# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor:  <farid at archlinux-br.org>

pkgname=supercollider
pkgver=3.4.4
pkgrel=1
pkgdesc="An environment and programming language for real time audio synthesis and algorithmic composition"
arch=('i686' 'x86_64')
url="http://supercollider.sourceforge.net/"
license=('GPL')
depends=('jack' 'fftw' 'icu' 'cwiid' 'curl')
makedepends=('scons' 'ruby' 'vim' 'emacs')
optdepends=('emacs: emacs interface'
            'gedit: gedit interface'
            'vim: vim interface'
            'ruby: vim support'
            'unhtml: vim support')
install="$pkgname.install"
source=("http://switch.dl.sourceforge.net/project/$pkgname/Source/$pkgver/SuperCollider-$pkgver-Source-linux.tar.gz")
md5sums=('0277d4a76268d2c534c68168bb2dedae')
_opts="PREFIX=/usr ALSA=yes AUDIOAPI=jack DEBUG=no DEVELOPMENT=no FFTW=yes LANG=yes LID=yes NO_LIBSNDFILE=no \
       WII=true RENDEZVOUS=yes SCEL=yes SCVIM=yes SCED=yes TERMINAL_CLIENT=yes CURL=yes READLINE=yes X11=yes"

build() {
  cd "$srcdir/SuperCollider-Source/common"

  # Fix sclangpipe_app path in scvimrc
  sed -i "s|local/||" ../editors/scvim/scvimrc

  # Stripping Debian related code
  sed -i "34,52d" ../editors/scvim/bin/scvim

  # utf-8 encoding fix for ruby 1.9
  sed -i "s|\(ruby\).*|\1 -Kn|" ../editors/scvim/bin/scvim_make_help

  # fix readline header check in scons
  sed -i "s|(\('readline/readline.h'\)|(['stdio.h', \1]|" SConstruct

  # remove insecure rpath
  sed -i "s|-Wl,-rpath.*'|'|" SConstruct

  scons PREFIX=/usr $_opts
}

package() {
  cd "$srcdir/SuperCollider-Source/common"

  scons DESTDIR="$pkgdir/" $_opts install

  # Installing vim files
  scons -C ../editors/scvim PREFIX="$pkgdir/usr" VIMFILE_DIR=/usr/share/vim/addons install-system

  install -d "$pkgdir/usr/share/vim/vimfiles"
  cp -a ../editors/scvim/{extra,ftplugin,indent,syntax} \
    "$pkgdir/usr/share/vim/vimfiles"
}
