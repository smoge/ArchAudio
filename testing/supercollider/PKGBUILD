# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor:  <farid at archlinux-br.org>

pkgname=supercollider
pkgver=3.4.3
pkgrel=1
pkgdesc="An environment and programming language for real time audio synthesis and algorithmic composition"
arch=('i686' 'x86_64')
url="http://supercollider.sourceforge.net/"
license=('GPL')
depends=('jack' 'avahi' 'fftw' 'icu')
makedepends=('scons' 'vim' 'emacs' 'ruby')
optdepends=('emacs: emacs interface'
            'gedit: gedit interface'
            'vim: vim interface'
            'ruby: vim support'
            'unhtml: vim support')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/$pkgname/Source/$pkgver/SuperCollider-$pkgver-Source-linux.tar.gz")
md5sums=('a7d5371c01181b25e476c0e5c5dfe0b6')

build() {
  cd "$srcdir/SuperCollider-Source/common"

  # Fix sclangpipe_app path in scvimrc
  sed -i "s|local/||" ../editors/scvim/scvimrc

  # Stripping Debian related code
  sed -i "34,52d" ../editors/scvim/bin/scvim

  # utf-8 encoding fix for ruby 1.9
  sed -i "s|ruby|& -Kn|" ../editors/scvim/bin/scvim_make_help

  scons PREFIX=/usr X11=yes SCVIM=yes SCEL=yes SCED=yes LID=yes READLINE=no WII=no
}

package() {
  cd "$srcdir/SuperCollider-Source/common"

  scons DESTDIR="$pkgdir/" \
        PREFIX=/usr X11=yes SCVIM=yes SCEL=yes SCED=yes LID=yes READLINE=no WII=no install

  # Installing vim files
  scons -C ../editors/scvim PREFIX="$pkgdir/usr" VIMFILE_DIR=/usr/share/vim/addons install-system

  install -d "$pkgdir/usr/share/vim/vimfiles"
  cp -a ../editors/scvim/{extra,ftplugin,indent,syntax} \
    "$pkgdir/usr/share/vim/vimfiles"
}
