# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=qutecsound
pkgver=0.6.1
pkgrel=2
pkgdesc="A cross platform editor and front-end for Csound with syntax highlighting,"
arch=('i686' 'x86_64')
url="http://qutecsound.sourceforge.net/"
license=('GPL')
depends=('csound' 'qt')
optdepends=('csound-doc: built-in help')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/qutecsound/QuteCsound/$pkgver/$pkgname-$pkgver-src.tar.gz"
        "$pkgname.desktop")
md5sums=('e480ccccdbc038e928800c9820221af5'
         '061af6b0b3ac06b2f27f44871ec7f58b')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  
  qmake qcs.pro
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # Prepare folders
  install -d $pkgdir/usr/{bin,share/{applications,{,doc}/$pkgname}}

  # Bin file
  install -Dm755 bin/qutecsound-f "$pkgdir/usr/bin/$pkgname"

  # Examples docs and data
  cp -a examples $pkgdir/usr/share/$pkgname
  cp -a images $pkgdir/usr/share/$pkgname
  cp doc/* $pkgdir/usr/share/doc/$pkgname

  # Desktop file and pixmaps
  install -Dm644 ../$pkgname.desktop "$pkgdir/usr/share/applications"
  install -Dm644 images/qtcs.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

