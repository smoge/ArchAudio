# Maintainer: Arch Linux Pro Audio <dev@archaudio.org>
# Contributor: Ray Rashif <schivmeister@gmail.com>

pkgname=jack
_pkgname=jack-audio-connection-kit
pkgver=0.116.2
pkgrel=3
pkgdesc="Jack Audio Connection Kit: A low-latency audio server"
arch=(i686 x86_64)
url="http://jackaudio.org/"
license=('GPL' 'LGPL')
groups=('archaudio-minimal' 'archaudio-suite')
depends=('libsndfile')
makedepends=('doxygen' 'ffado' 'libsamplerate' 'celt')
optdepends=('ffado: FireWire audio device support'
	    'libsamplerate, celt: NetJACK driver')
provides=("jack-audio-connection-kit=$pkgver")
conflicts=('jack-audio-connection-kit')
backup=(etc/conf.d/$pkgname)
options=('!libtool')
install=$pkgname.install
source=(http://jackaudio.org/downloads/$_pkgname-$pkgver.tar.gz
	jackconf
	jackinit)

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure --prefix=/usr
  make || return 1
  make DESTDIR="$pkgdir/" install

  mv "$pkgdir/usr/share/$_pkgname" "$pkgdir/usr/share/$pkgname"

  install -Dm755 ../jackinit "$pkgdir/etc/rc.d/$pkgname"
  install -Dm644 ../jackconf "$pkgdir/etc/conf.d/$pkgname"
}
md5sums=('9c0ae9880e9b6e081f1a238fe6a28bd5'
         '12b1017f6f06248e2ddd4a28dfae87d4'
         '6cd8a8a42a756d3ba0d4842bba171ca3')
