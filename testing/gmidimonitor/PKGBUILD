# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=gmidimonitor
pkgver=3.5
pkgrel=1
pkgdesc="A GTK+ application that shows MIDI events."
arch=('i686' 'x86_64')
url="http://home.gna.org/gmidimonitor/"
license=('GPL')
depends=('lash' 'libglade')
makedepends=('python2')
install="$pkgname.install"
source=("http://download.gna.org/$pkgname/$pkgname-$pkgver.tar.bz2"
        "$pkgname.svg::http://upload.wikimedia.org/wikipedia/commons/d/d5/RB_keyboard_icon.svg"
        "$pkgname.desktop")
md5sums=('56f51168f03b0f59f81aa710938f6f70'
         'e6e764ea13f2b15a297d8cd62540edf2'
         '4fb243314630c7be57872c5c76c2e48a')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf configure --prefix=/usr
  python2 waf
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf install --destdir="$pkgdir/"

  # pixmaps and desktop file
  install -Dm644 ../$pkgname.desktop \
          "$pkgdir/usr/share/applications/$pkgname.desktop"

  install -Dm644 ../$pkgname.svg \
          "$pkgdir/usr/share/pixmaps/$pkgname.svg"
}

