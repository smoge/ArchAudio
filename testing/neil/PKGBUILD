# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=neil
pkgver=0.13_hg
pkgrel=1
pkgdesc="A modular Buzz-like tracker, fork of aldrin."
arch=(i686 x86_64)
url="http://sites.google.com/site/neilsequencer/"
license=('GPL')
depends=('python2' 'liblo' 'libmad' 'jack' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('scons')
provides=('libneil' 'libneil-hg')
conflicts=('neil-hg' 'libneil' 'libneil-hg')
install="$pkgname.install"
source=("https://bitbucket.org/bucket_brigade/$pkgname/get/tip.tar.bz2")
md5sums=(`wget -qO- $source | md5sum | cut -c -32`)

build() {
  cd `find $srcdir -mindepth 1 -maxdepth 1 -type d`
  
  # python2 fixes
  export PYTHON="python2"
  sed -i "s|\(env python\).*|\12|;s|\(bin/python\).*|\12|" \
    `grep -Erl "(env python|bin/python)" .`
  sed -e "s|'python |'python2 |g" \
      -e "s|--rpath|-Wl,-rpath=|" \
      -i libneil/SConscript # GCC 4.6 fix

  scons configure PREFIX=/usr
  scons
}

package() {
  cd `find $srcdir -mindepth 1 -maxdepth 1 -type d`

  scons install DESTDIR="$pkgdir/"
  
  # fix python2 libs path in path.cfg
  sed -i "s|\(site_packages = \).*\(/usr/lib/python2.7/site-packages\)|\1\2|" \
         "$pkgdir/etc/neil/path.cfg"
}

# vim:set ts=2 sw=2 et:
