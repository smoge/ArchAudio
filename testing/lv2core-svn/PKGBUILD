# Maintainer: Arch Linux Pro Audio <dev@archaudio.org>
# Contributor: Ray Rashif <schivmeister@gmail.com>

pkgname=lv2core-svn
pkgver=2430
pkgrel=1
pkgdesc="LV2: Successor to the LADSPA audio plug-in standard (development version)"
arch=(i686 x86_64)
url="http://lv2plug.in/"
license=('LGPL')
makedepends=('subversion' 'python')
provides=('lv2core')
conflicts=('lv2core')
source=()
md5sums=()

_svntrunk=http://svn.drobilla.net/lad/trunk
_svnmod=lad

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build/lv2/core.lv2"

  ./waf configure --prefix=/usr
  ./waf build $MAKEFLAGS || return 1
  ./waf install --destdir="$pkgdir"
}
