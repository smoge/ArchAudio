# Maintainer: Arch Linux Pro Audio <dev@archaudio.org>
# Contributor:  M0Rf30

pkgname=qjackctl-svn
pkgver=673
pkgrel=1
pkgdesc="A qt based frontend to the jack server (jack-audio-connection-kit)"
arch=('i686' 'x86_64')
license=('GPL' 'openssl')
depends=('jack' 'qt' 'subversion')
options=('!libtool')
url="http://qjackctl.sourceforge.net/"
makedepends=('subversion')
provides=(qjackctl)
conflicts=(qjackctl-cvs)
source=(patch)
md5sums=('4ebdc1315469c6d6d8152c1de4b0cd6f')
_svntrunk=https://qjackctl.svn.sourceforge.net/svnroot/qjackctl/trunk
_svnmod=qjackctl-svn

build() {

 cd ${srcdir}
  rm -rf $srcdir/build

  if [ -d ${_svnmod} ]; then
    msg "Updating SVN tree ..."
    cd ${_svnmod}
    svn revert -R .
    svn up -r $pkgver
    msg "SVN update finished, starting build ..."
  else
    msg "Doing SVN checkout ..."
    svn co ${_svntrunk} ${_svnmod} -r $pkgver
    cd ${_svnmod}
    msg "SVN checkout finished, starting build ..."
  fi

  
  cp -r $srcdir/${_svnmod} $srcdir/build
  cd $srcdir/build
 
  autoheader
  autoconf
  date > stamp-h.in 
  ./configure --prefix=/usr
   patch -p1 -i ../patch
  make
  make DESTDIR=${pkgdir} install
}
