# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=nsound
pkgver=0.7.5
pkgrel=1
pkgdesc="A C++ framework with python bindings for audio synthesis. It aims to be as powerful as Csound but with the programming features of C++."
arch=(i686 x86_64)
url="http://nsound.sourceforge.net/"
license=('GPL')
depends=('python-matplotlib')
makedepends=('scons' 'swig')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('154e3474f6248da2d13c1154621a130d')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Python2 fixes
  export PYTHON="python2"
  sed "s|env python|&2|;s|bin/python|&2|" \
    -i `grep -rlE "(env python|bin/python)" .`

  # Optimization
  export CXXFLAGS="-fno-strict-aliasing -fwrapv -O2"

  # Scons script fixes
  sed -i "s|Resulut|Result|" NsoundConfig.py
  sed -i "s|dir = \"\"|dir = \"$pkgdir/usr\"|g;/RPATH/d;/if encode/d" SConstruct


  # Build
  scons setup.py --disable-openmp
  python2 setup.py build
  scons install
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # Install python bindings
  python2 setup.py install --root="$pkgdir/"

  mkdir -p "$pkgdir"/usr/{bin,lib,share/{doc/Nsound,Nsound/examples},include/Nsound}

  # Install bins and libs
  install -Dm755 bin/* "$pkgdir/usr/bin"
  install -Dm755 lib/* "$pkgdir/usr/lib"

  # Install Wavelab
  cp -a applications/wavelab "$pkgdir/usr/share/Nsound"

  cat << EOF >> "$pkgdir/usr/bin/wavelab"
#!/bin/bash
cd /usr/share/Nsound/wavelab && python2 Wavelab.py
EOF

  chmod +x "$pkgdir/usr/bin/wavelab"

  # Install headers and docs
  install -Dm644 src/Nsound/*.h "$pkgdir/usr/include/Nsound"
  install -Dm644 docs/*.{odt,pdf} "$pkgdir/usr/share/doc/Nsound"

  # Download and install examples
  cd src/examples && ./getWavefiles.bash
  install -Dm644 *.{cc,py,wav} "$pkgdir/usr/share/Nsound/examples"
}

# vim:set ts=2 sw=2 et:
