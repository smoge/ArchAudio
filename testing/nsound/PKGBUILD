# Maintainer: SpepS <dreamspepser at yahoo dot it>

_p=Nsound
pkgname=nsound
pkgver=0.8.1
pkgrel=1
pkgdesc="A C++ framework with python bindings for audio synthesis that aims to be as powerful as Csound but with the programming features of C++."
arch=(i686 x86_64)
url="http://nsound.sourceforge.net/"
license=('GPL')
depends=('python2-matplotlib' 'libao' 'portaudio')
makedepends=('scons' 'swig')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz"
        "http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgname-$pkgver/$_p-$pkgver-Users_Guide.pdf")
md5sums=('f12384b0bde5200153a47554a60caa01'
         'e527bb83881f5ac74b7733e5bb1eecef')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Python2 fixes
  export PYTHON="python2"
  sed "s|env python|&2|;s|bin/python|&2|" \
    -i `grep -rlE "(env python|bin/python)" .`

  # Optimization
  export CXXFLAGS="-fno-strict-aliasing -fwrapv -O2"

  # Remove insecure rpath
  sed -i "/rpath/d" NsoundConfig.py

  # Build
  scons setup.py --disable-openmp
  python2 setup.py build
  scons install
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # Install python bindings
  python2 setup.py install --root="$pkgdir/"

  # prepare dirs
  install -d "$pkgdir"/usr/{bin,lib,share/{doc/$_p,$_p/examples},include/$_p}

  # Install bins and libs
  install -Dm755 bin/* "$pkgdir/usr/bin"
  install -Dm755 lib/* "$pkgdir/usr/lib"

  # Install Wavelab
  cp -a applications/wavelab "$pkgdir/usr/share/$_p"

  cat << EOF >> "$pkgdir/usr/bin/wavelab"
#!/bin/bash
cd /usr/share/$_p/wavelab && python2 Wavelab.py
EOF

  chmod +x "$pkgdir/usr/bin/wavelab"

  # Install headers and docs
  install -Dm644 src/$_p/*.h "$pkgdir/usr/include/$_p"
  install -Dm644 docs/*.odt "$pkgdir/usr/share/doc/$_p"
  install -Dm644 ../$_p-$pkgver-Users_Guide.pdf "$pkgdir/usr/share/doc/$_p"

  # Download and install examples
  cd src/examples && ./getWavefiles.bash
  install -Dm644 *.{cc,py,wav} "$pkgdir/usr/share/$_p/examples"
}

# vim:set ts=2 sw=2 et:
