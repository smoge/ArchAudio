# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=bigband
pkgver=1.2
pkgrel=3
pkgdesc="A program to compose real music for small ensembles"
arch=(i686 x86_64)
url="http://members.chello.nl/w.boeke/bigband/"
license=('GPL')
depends=('sdl_ttf' 'sdl_gfx' 'jack' 'gcc-libs')
install="$pkgname.install"
source=("$url$pkgname-$pkgver.tar.gz"
        "$pkgname.desktop")
md5sums=('0df600e43b4ff63a1f0742ae481866b6'
         'ee3541004c2575767940199914135225')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --fontdir /usr/share/fonts/TTF
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -d "$pkgdir/usr/bin"
  make SDIR="$pkgdir/usr/share" BIN_DIR="$pkgdir/usr/bin" install

  # desktop file and pixmaps
  install -Dm644 ../$pkgname.desktop \
          "$pkgdir/usr/share/applications/$pkgname.desktop"

  install -Dm644 src/bb-icon.xpm \
          "$pkgdir/usr/share/pixmaps/$pkgname.xpm"
}

# vim:set ts=2 sw=2 et:
