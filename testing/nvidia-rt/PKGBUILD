# original Maintainer : Thomas Baechler <thomas@archlinux.org>
# Maintainer for kernel26-rt : Morgan Cox <morgancoxuk@gmail.com>

pkgname=nvidia-rt
pkgver=260.19.29
_kernver='2.6.33-rt'
pkgrel=1
pkgdesc="NVIDIA drivers for kernel26."
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('kernel26>=2.6.33' 'kernel26<2.6.38' "nvidia-utils=${pkgver}")
makedepends=('kernel26-headers>=2.6.33' 'kernel26-headers<2.6.38')
conflicts=('nvidia-96xx' 'nvidia-173xx')
license=('custom')
install=nvidia.install

if [ "$CARCH" = "i686" ]; then
	_arch='x86'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
	source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
	nvidiart.patch
)

elif [ "$CARCH" = "x86_64" ]; then
	_arch='x86_64'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
	source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
	nvidiart.patch
)

fi

build() {
	cd $srcdir
	sh ${_pkg}.run --extract-only
	cd ${_pkg}/kernel
	patch < $srcdir/nvidiart.patch
        make SYSSRC=/lib/modules/${_kernver}/build module
}

package() {
        install -D -m644 $srcdir/${_pkg}/kernel/nvidia.ko \
                $pkgdir/lib/modules/${_kernver}/kernel/drivers/video/nvidia.ko
        install -d -m755 $pkgdir/etc/modprobe.d
        echo "blacklist nouveau" >> $pkgdir/etc/modprobe.d/nouveau_blacklist.conf
        sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" $startdir/nvidia.install
}


md5sums=('0583668d86f7318d99def3ed3e1cc4bc'
         '8f57481f4ae28fb1ec5a5f3af366cceb')
