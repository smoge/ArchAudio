# Contributor: SpepS <dreamspepser at yahoo dot it>

pkgname=ulatencyd
pkgver=0.3.5
pkgrel=1
pkgdesc="Daemon to minimize latency on a linux system using cgroups"
arch=(i686 x86_64)
url="https://github.com/poelzi/ulatencyd"
license=('GPL')
depends=('lua' 'luaposix' 'dbus-glib' 'libxcb')
makedepends=('cmake')
conflicts=("$pkgname-git")
backup=("etc/$pkgname/$pkgname.conf")
source=("https://github.com/downloads/poelzi/$pkgname/$pkgname-$pkgver.tar.gz"
        "$pkgname.d"
        "$pkgname")

md5sums=('f46fee842af7da66283f73175a0c19b6'
         'ad0369cfa0deac8b5fe30dacc5a4e7c2'
         '0694379b7aa5ecb129b1276f49b4bc24')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # Docs
  install -d "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 README docs/* "$pkgdir/usr/share/doc/$pkgname"

  # Daemon script
  install -Dm755 ../$pkgname "$pkgdir/etc/rc.d/$pkgname"
  install -Dm644 ../$pkgname.d "$pkgdir/etc/conf.d/$pkgname"
}

# vim:set ts=2 sw=2 et:
