# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=ulatencyd
pkgver=0.4.9
pkgrel=1
pkgdesc="Daemon to minimize latency on a linux system using cgroups"
arch=(i686 x86_64)
url="https://github.com/poelzi/ulatencyd"
license=('GPL')
depends=('lua' 'luaposix' 'dbus-glib' 'libxcb')
optdepends=('python2-qt: qt gui')
makedepends=('cmake')
conflicts=("$pkgname-git")
backup=("etc/$pkgname/$pkgname.conf")
source=("https://github.com/downloads/poelzi/$pkgname/$pkgname-$pkgver.tar.gz"
        "$pkgname.d"
        "$pkgname")
md5sums=('fe3fc664e4c0d0c7d20f9906e5631545'
         'ceb27033cb79d2a9ad71e390b0b75d7d'
         '512bd9215650c95c04717abf8a2d2f40')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Disable systemd service
  sed -i "180,181d" CMakeLists.txt

  # Python 2 fix for pyqt gui
  sed -i "s|\(env python\).*|\12|" client/ulatency

  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # Docs
  install -d "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 README docs/* "$pkgdir/usr/share/doc/$pkgname"

  # Daemon script
  install -Dm755 ../$pkgname "$pkgdir/etc/rc.d/$pkgname"
  install -Dm644 ../$pkgname.d "$pkgdir/etc/conf.d/$pkgname"
}

# vim:set ts=2 sw=2 et:
