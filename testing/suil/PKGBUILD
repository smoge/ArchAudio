# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=suil
pkgver=0.4.2
pkgrel=1
pkgdesc="A lightweight C library for loading and wrapping LV2 plugin UIs."
arch=(i686 x86_64)
url="http://drobilla.net/software/$pkgname/"
license=('custom:ISC')
depends=('qt' 'gtk2')
makedepends=('python2' 'lv2-ui')
conflicts=("$pkgname-svn")
source=("http://download.drobilla.net/$pkgname-$pkgver.tar.bz2")
md5sums=('43b0b4ed14efb3fbdeb18176546c5c17')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # remove ldconfig
  sed -i "/ldconfig/d" wscript

  python2 ./waf configure --prefix=/usr \
                          --mandir=/usr/share/man
  python2 ./waf
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  DESTDIR="$pkgdir" python2 ./waf install

  # license
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
