# Maintainer : Philipp Ãœberbacher <murks at lavabit dot com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: SpepS <dreamspepser at yahoo dot it>

pkgname=zita-convolver
pkgver=2.0.0
pkgrel=4
pkgdesc="Convolution library for aeolus and jconvolver"
arch=('i686' 'x86_64')
url="http://kokkinizita.linuxaudio.org/linuxaudio"
license=('GPL')
depends=('fftw')
source=("http://kokkinizita.linuxaudio.org/linuxaudio/downloads/$pkgname-$pkgver.tar.bz2")
md5sums=('7e4e30fb628aed009fa22e4ed46f556a')

build() {
  cd "$srcdir/$pkgname-$pkgver/libs"

  sed -i 's/usr\/local/usr/' Makefile
  sed -i 's/ldconfig//' Makefile
  sed -i 's/i686/native/' Makefile

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/libs"

  make SUFFIX="" PREFIX="$pkgdir/usr" install

  ln -s lib$pkgname.so.$pkgver \
    "$pkgdir/usr/lib/lib$pkgname.so.${pkgver%%.*}"
}
