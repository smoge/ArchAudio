# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: farid abdelnour <farid at archlinux-br.org>

pkgname=vmpk
pkgver=0.4.0
pkgrel=1
pkgdesc="MIDI event generator and receiver"
arch=('i686' 'x86_64')
url="http://vmpk.sourceforge.net/"
license=('GPL3')
depends=('qt' 'jack')
makedepends=('cmake')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.tar.bz2")
md5sums=('bc54ac39fe02ce3cbed4b2b7f82cd70f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  [ -d build ] || mkdir build && cd build

  # vmpk with jack rtmidi
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DRTMIDI_DRIVER=JACK \
           -DENABLE_DBUS=1 \
           -DCMAKE_SKIP_RPATH=ON
  make

  # rename vmpk to vmpk-jack
  mv src/$pkgname src/$pkgname-jack

  # vmpk with alsa rtmidi
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DRTMIDI_DRIVER=ALSA \
           -DENABLE_DBUS=1 \
           -DCMAKE_SKIP_RPATH=ON
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/build"

  make DESTDIR="$pkgdir/" install

  # jack enabled vmpk bin
  install -Dm755 src/$pkgname-jack "$pkgdir/usr/bin/$pkgname-jack"

  # vmpk-jack desktop file
  sed "s|VMPK|& Jack|;s|Exec.*|&-jack|" ../$pkgname.desktop > \
    "$pkgdir/usr/share/applications/$pkgname-jack.desktop"
}
