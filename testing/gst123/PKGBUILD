# Maintainer: SpepS <dreamspepser at yahoo dot it>
# Contributor: paterbrown <paterbrown at silberhelme dot de>

pkgname=gst123
pkgver=0.2.1
pkgrel=1
pkgdesc="A more flexible command line player in the spirit of ogg123 and mpg123, based on gstreamer."
arch=('i686' 'x86_64')
url="http://space.twc.de/~stefan/gst123.php"
license=('LGPL')
depends=('gstreamer0.10-base' 'gtk2')
makedepends=('ncurses')
optdepends=('gstreamer0.10-good: more codecs support'
            'gstreamer0.10-bad: more codecs support'
            'gstreamer0.10-ugly: more codecs support')
source=("http://space.twc.de/~stefan/$pkgname/$pkgname-$pkgver.tar.bz2")
md5sums=('956d8c28e26322286b11688226450ca5')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # replace ncurses5 with ncursesw5
  sed -i "s_ncurses5_ncursesw5_g" configure*

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
