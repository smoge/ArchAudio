# Maintainer: SpepS <dreamspepser at yahoo dot it>
# Contributor: paterbrown <paterbrown at silberhelme dot de>

pkgname=gst123
pkgver=0.1.4
pkgrel=1
pkgdesc="A more flexible command line player in the spirit of ogg123 and mpg123, based on gstreamer."
arch=('i686' 'x86_64')
url="http://space.twc.de/~stefan/gst123.php"
license=('LGPL')
depends=('gstreamer0.10-base' 'gtk2')
makedepends=('ncurses')
optdepends=('gstreamer0.10-good' 'gstreamer0.10-bad' 'gstreamer0.10-ugly')
source=("http://space.twc.de/~stefan/gst123/${pkgname}-${pkgver}.tar.bz2")
md5sums=('a89b257730f7170dda048984ca2339f4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i "s_ncurses5_ncursesw5_g" configure*

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
