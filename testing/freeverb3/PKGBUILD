# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=freeverb3
pkgver=2.5.16
pkgrel=1
pkgdesc="A sound processing library which includes sampling rate scaling enabled version of freeverb with a lot of extensions."
arch=(i686 x86_64)
url="http://freeverb3.sourceforge.net/"
license=('GPL')
depends=('fftw' 'jack')
makedepends=('audacious' 'bmp' 'xmms')
optdepends=('audacious: audacious plugin'
            'bmp: beep media player plugin'
            'xmms: xmms plugin')
options=('!libtool')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('3e448633d784a0c0bb5638b9b5ecce62')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # remove unsecure rpath
  sed -i "s|\${wl}\-rpath \${wl}\$libdir||" configure

  ./configure --prefix=/usr \
              --enable-release \
              --enable-double \
              --enable-sample \
              --enable-jack \
              --enable-xmms \
              --enable-bmp \
              --enable-plugdouble \
              --enable-audacious \
              --disable-rpath
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
