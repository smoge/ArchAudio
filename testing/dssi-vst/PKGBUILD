# Maintainer: Arch Linux Pro Audio <dev@archaudio.org>
# Contributor: Ray Rashif <schivmeister@gmail.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=dssi-vst
pkgver=0.8
pkgrel=2
pkgdesc="DSSI wrapper for Windows VST plug-ins"
arch=(i686)
url="http://dssi.sourceforge.net/"
license=('GPL')
depends=('dssi' 'wine' 'ladspa')
source=(http://downloads.sourceforge.net/dssi/$pkgname-$pkgver.tar.gz
	gcc44.patch
	winexec.patch)

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # gcc 4.4 compatibility
  patch -Np1 -i ../gcc44.patch || return 1

  # fix executable extension issue
  patch -Np1 -i ../winexec.patch || return 1

  # use CXXFLAGS in build
  sed -i "s:-Ivestige -Wall -fPIC:${CXXFLAGS} -Ivestige -Wall -fPIC:" \
    Makefile || return 1

  make || return 1
  make	BINDIR="$pkgdir/usr/bin" \
  	DSSIDIR="$pkgdir/usr/lib/dssi" \
	LADSPADIR="$pkgdir/usr/lib/ladspa" install

  # dssi-vst does not know about the executable extension issue
  cd "$pkgdir/usr/lib/dssi/dssi-vst"

  mv dssi-vst-server.exe dssi-vst-server
  mv dssi-vst-scanner.exe dssi-vst-scanner
}
md5sums=('2d46474eaf84f0a2efc2389edaa18e3a'
         '92d17c916852be3e8b62c13e65557928'
         '7578313fb2861165ed349cef1d02f829')
