# Contributor: Tom Vincent <http://www.tlvince.com/contact/>
# Contributor: SpepS <dreamspepser at yahoo dot it>

pkgname=jack-mixer
pkgver=9
pkgrel=3
pkgdesc="A GTK based Jack audio mixer"
url="http://home.gna.org/jackmixer/"
arch=('i686' 'x86_64')
license=('GPL2')
provides=('jack_mixer')
conflicts=('jack_mixer')
depends=('python2' 'python-fpconst' 'pygtk' 'jack' 'hicolor-icon-theme')
optdepends=('lash: session support'
            'python2-gconf: gconf support')
options=('!libtool')
install="$pkgname.install"
source=("http://download.gna.org/jackmixer/jack_mixer-$pkgver.tar.gz")
md5sums=('ee20bc3e76e20fcbb9a80ebba3d1c98c')

build() {

  cd "$srcdir/jack_mixer-$pkgver"

  #Python2 fixes
  export PYTHON="python2"
  sed -i "s_env python_&2_" jack_mixer.py

  ./configure \
    --prefix=/usr \
    --sysconfdir=/usr/share \
    --disable-schemas-install

  make
}

package() {

  cd "$srcdir/jack_mixer-$pkgver"
  make DESTDIR="$pkgdir/" install
}
