# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=lilv
pkgver=0.4.2
pkgrel=1
pkgdesc="A library to make the use of LV2 plugins as simple as possible for applications."
arch=(i686 x86_64)
url="http://drobilla.net/software/$pkgname/"
license=('custom:ISC')
depends=('python2' 'sord' 'jack')
makedepends=('lv2core' 'swig' 'lv2-event' 'lv2-uri-map')
optdepends=('bash-completion: auto-complete words')
conflicts=("$pkgname-svn")
source=("http://download.drobilla.net/$pkgname-$pkgver.tar.bz2")
md5sums=('ddba1703a313a785bdfc408580c37e82')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export PYTHON="/usr/bin/python2"

  # remove ldconfig
  sed -i "/ldconfig/d" wscript

  # fix var name for python bindings in wscript
  sed -i "s/BINDINGS/PYTHON/" wscript

  python2 ./waf configure --prefix=/usr \
                          --mandir=/usr/share/man \
                          --bindings
  python2 ./waf
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  DESTDIR="$pkgdir" python2 ./waf install

  # license
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
