# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=ayyi_auditioner
pkgver=0.1.0
pkgrel=1
pkgdesc="A service that is connected to Jack and can playback audio samples triggered via Alsa Midi and Dbus."
arch=(i686 x86_64)
url="http://www.ayyi.org/"
license=('GPL')
depends=('jack' 'dbus-glib')
options=('!libtool' '!emptydirs')
install="$pkgname.install"
source=("${url}files/$pkgname-$pkgver.tar.gz")
md5sums=('ee15218a271bb59e6e443669ba5423cc')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # let dssi plugin and desktop file be placed in right paths
  sed -e 's|$(data.*\(ayyi/auditioner\)|/usr/lib/dssi/\1|g' \
      -e 's|ayyi/clients|applications|g' \
      -i `find . -name "Makefile.*"`

  ./configure --prefix=/usr

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
