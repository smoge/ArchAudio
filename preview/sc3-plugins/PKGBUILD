# Maintainer: SpepS <dreamspepser at yahoo dot it>

_scver=3.4.4
pkgname=sc3-plugins
pkgver=2010.09.03
pkgrel=1
pkgdesc="Extension plugins for the SuperCollider3 audio synthesis server."
arch=(i686 x86_64)
url="http://sourceforge.net/projects/sc3-plugins/"
license=('GPL')
depends=("supercollider<=$_scver")
makedepends=('cmake')
source=("http://downloads.sourceforge.net/project/$pkgname/Source%20Release/$pkgname-src-${pkgver//./-}.tgz"
        "http://switch.dl.sourceforge.net/project/supercollider/Source/$_scver/SuperCollider-$_scver-Source-linux.tar.gz")
md5sums=('81d990ed7664fa4843e67417cf9cbbc9'
         '0277d4a76268d2c534c68168bb2dedae')

build() {
  cd "$srcdir/$pkgname"

  scons SC3PATH="$srcdir/SuperCollider-Source/common/" QUARKS=0 PREFIX=/usr
}

package() {
  cd "$srcdir/$pkgname"

  scons install DESTDIR="$pkgdir/" PREFIX=/usr

  # install plugins to /usr/lib instead of /usr/share
  install -d "$pkgdir/usr/lib/SuperCollider/plugins"
  mv "$pkgdir/usr/share/SuperCollider/Extensions/SC3plugins/"*.so \
     "$pkgdir/usr/lib/SuperCollider/plugins"
}

# vim:set ts=2 sw=2 et:
