# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=synthclone
pkgver=0.0.4
pkgrel=3
pkgdesc="A GTK-based application that works with JACK to "clone" your MIDI-capable instruments."
arch=('i686' 'x86_64')
url="http://code.google.com/p/synthclone/"
license=('GPL')
depends=('gtkmm' 'jack' 'phat')
makedepends=('scons')
install="$pkgname.install"
source=("http://synthclone.googlecode.com/files/$pkgname-$pkgver.tar.gz"
        "http://synthclone.googlecode.com/svn/trunk/data/synthclone.ui"
        "http://synthclone.googlecode.com/svn/trunk/src/sampler.cpp"
        "http://synthclone.googlecode.com/svn/trunk/src/sampler.hpp"
        "$pkgname.png")
md5sums=('a9ca5cdd46a2c0afff3c3833391f70a0'
         'e046e2a73e5dfe8845e6ce0ed6679534'
         '56cddd1b1d4cea1fdb0a9b93ac356090'
         '07cff4770604b40d141b5ae5a1e0a9f0'
         '3b40e6051ffcb1d43de4bbd961f812cf')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  
  # Replacing bugged code
  cp ../sampler.{h,c}pp src
  cp ../synthclone.ui data

  # Adding icon in desktop file
  sed -i "12aIcon=$pkgname" templates/$pkgname.desktop

  scons prefix=/usr
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # Install
  install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm644 data/$pkgname.ui \
    "$pkgdir/usr/share/$pkgname/$pkgname.ui"

  # Install desktop file and pixmaps (from opencliparts.org)
  install -Dm644 data/$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 ../$pkgname.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
