# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: N30N <archlinux@alunamation.com>

pkgname=xjadeo
pkgver=0.6.2
pkgrel=1
pkgdesc="A simple video player that is synchronized to jack transport."
arch=('i686' 'x86_64')
url="http://xjadeo.sourceforge.net"
license=('GPL')
depends=('ffmpeg' 'imlib2' 'jack' 'liblo' 'libxpm' 'portmidi')
makedepends=('imlib' 'qt')
optdepends=('qt: for the GUI (qjadeo)')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz"
	"qjadeo.desktop")
install="$pkgname.install"
md5sums=('b8fef817322a08c8b3ab19498885c777'
         '315e2ab44ce3edf4068ff6db48942908')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  install -Dm644 doc/xjadeorc "$pkgdir/etc/xjadeorc"
  install -Dm644 doc/xjadeo.png "$pkgdir/usr/share/pixmaps/qjadeo.png"
  install -Dm644 ../qjadeo.desktop "$pkgdir/usr/share/applications/qjadeo.desktop"
}

# vim: set noet ff=unix: