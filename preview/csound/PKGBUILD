# Maintainer : Arch Linux Pro Audio <dev@archaudio.org>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Bernardo Barros <bernardobarros@gmail.com>

pkgname=csound
pkgver=5.14.2
pkgrel=1
pkgdesc="A sound and music synthesis system, providing facilities for composition and performance over a wide range of platforms."
arch=(i686 x86_64)
url="http://csound.sourceforge.net/"
license=('GPL')
depends=('python2' 'fltk' 'fluidsynth' 'liblo' 'portaudio' 'portmidi' 'tk' 'lua')
makedepends=('scons' 'swig' "java-environment>=6" 'dssi' 'boost' 'pd')
optdepends=('qutecsound: qt frontend'
            'cecilia: tcl/tk frontend'
            'cecilia4: wxpython frontend'
            'vim: vim frontend'
            'java-environment: java wrapper')
provides=('csound5')
conflicts=('csound5')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}5/${pkgname}${pkgver%.*}/Csound${pkgver}.tar.gz"
        "http://ccrma.stanford.edu/software/stk/release/stk-4.4.2.tar.gz") # needed by stk opcodes
md5sums=('053c364620b778ab5def18d0f8c4db48'
         '0b7bd1f913ca01fb8b2201ebcf853358')
_csopts="useDouble=1 usePortAudio=1 usePortMIDI=1 useALSA=1 useJack=1 useFLTK=1
         noFLTKThreads=1 pythonVersion=2.7 buildCsoundVST=0 buildCsoundAC=1
         buildCsound5GUI=1 buildLoris=0 useOSC=1 bufferoverflowu=0 useUDP=1
         buildPythonOpcodes=1 buildLuaOpcodes=1 prefix=/usr instdir="$pkgdir/"
         buildRelease=1 noDebug=1 gcc3opt=0 gcc4opt=native useLrint=0 useGprof=0
         Word64=1 Lib64=0 dynamicCsoundLibrary=1 buildStkOpcodes=1 install=0
         buildPDClass=1 useCoreAudio=0 useAltivec=0 buildDSSI=1 buildUtilities=1
         buildTclcsound=1 buildWinsound=0 buildVirtual=1 buildInterfaces=1
         buildLuaWrapper=1 buildPythonWrapper=1 buildJavaWrapper=1 buildOSXGUI=0
         buildCSEditor=1 withICL=0 withMSVC=0 withSunStudio=0 buildNewParser=1
         NewParserDebug=0 buildMultiCore=1 buildvst4cs=0 useGettext=1
         buildImageOpcodes=1 useOpenMP=0 buildOLPC=0 tclversion=8.5 includeMP3=0
         includeWii=0 includeP5Glove=0 buildBeats=1 buildcatalog=1 includeSerial=1"

build() {
  cd "$srcdir/Csound$pkgver"

  # fix pd install path, remove uninstaller and ldconfig call
  sed -e "s|/usr/local/lib|$pkgdir/usr/lib|" \
      -e "/create/,/^print ''/d" \
      -e "/getuid/,/ldconfig/d" \
      -i install.py

  # add custom java include path, remove fltk include path
  sed -e '/\(usr\/\)\(include\)\/fltk-1.1/s//\1lib\/jvm\/java-6-openjdk\/\2/p;/6/s//7/p;' \
      -e '/openjdk\/include/s//&\/linux/p;/7/s//6/' custom.py.mkg > custom.py

  # Add build optimization and java include path fixes, fix lua link
  sed -e "s_mtune_march_g" \
      -e "s|,.*5.0'||" \
      -e "s|lua51|lua|g" \
      -i SConstruct

  # place stk sources for the stk opcodes
  cp -a "$srcdir"/stk-4.4.2/{include,rawwaves,src} Opcodes/stk

  # Python2 fixes
  sed "s_\(env python\).*_\12_;s_\(bin/python\).*_\12_" \
    -i `grep -rlE "(env python|bin/python)" .`

  scons $_csopts
}

package() {
  cd "$srcdir/Csound$pkgver"

  python2 install.py --vimdir="/usr/share/vim/vimfiles" \
                     --instdir="$pkgdir" \
	             --prefix="/usr"

  # Link libraries
  ln -s /usr/lib/libcsound64.so "$pkgdir/usr/lib/libcsound.so"
  ln -s /usr/lib/libcsound64.so "$pkgdir/usr/lib/libcsound.so.5.2"

  # Install lua bindings
  install -d "$pkgdir/usr/lib/lua/5.1"
  install -Dm 755 luaC{snd,soundAC}.so "$pkgdir/usr/lib/lua/5.1"
  ln -s /usr/lib/lua/5.1/luaCsnd.so "$pkgdir/usr/lib/luaCsnd.so"

  # Install python CsoundAC bindings
  install -D _CsoundAC.so CsoundAC.py "$pkgdir/usr/lib/python2.7/site-packages"

  # Install examples
  install -d "$pkgdir/usr/share/$pkgname"
  cp -a examples "$pkgdir/usr/share/$pkgname"
  cp -a samples "$pkgdir/usr/share/$pkgname"
}

# vim:set ts=2 sw=2 et:
