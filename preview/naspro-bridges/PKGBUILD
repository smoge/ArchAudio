# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=naspro-bridges
pkgver=0.3.0
pkgrel=1
pkgdesc="A collection of bridges to LV2 which allow you to use plugins developed for other plugin standards in LV2 hosts."
arch=('i686' 'x86_64')
url="http://naspro.atheme.org/"
license=('GPL')
groups=('naspro')
depends=("naspro-bridge-it>=$pkgver" 'alsa-lib')
makedepends=('dssi' 'ladspa' 'lv2-dynmanifest' 'lv2-event' 'lv2-uri-map')
optdepends=('ladspa: lapspa bridges'
            'dssi: dssi bridges'
            'lv2-events: dssi bridges'
            'lv2-uri-map: dssi bridges')
options=('!libtool')
source=("http://switch.dl.sourceforge.net/project/naspro/naspro/$pkgver/$pkgname-$pkgver.tar.bz2")
md5sums=('e83bcde2bb6ac2cd83363a3ec1e51605')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # lv2-dyn-manifest
  sed -i 's/dyn-manifest/dynmanifest/g' `grep -rl dyn-manifest .`

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
