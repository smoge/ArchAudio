# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=rtmidi
pkgver=1.0.15
pkgrel=1
pkgdesc="A set of C++ classes that provides a common API for realtime MIDI input/output."
arch=(i686 x86_64)
url="http://www.music.mcgill.ca/~gary/rtmidi/"
license=('GPL')
depends=('jack')
source=("${url}release/$pkgname-$pkgver.tar.gz"
        "$pkgname-$pkgver-shared.patch"
        "$pkgname-jack.pc"
        "$pkgname.pc"
        "Makefile")
md5sums=('a500b0a80dbf0a0f4a9b69cd7af134e2'
         'd5f52e293e1b9811259b30ee6dec3f1e'
         'ee51427cb1d93d06a16cda7de4e59f80'
         '03f64857639e4748b27bc3a393e59fcc'
         'c803f67240480c63fe8d31e4d8ad890c')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # enable shared lib build
  patch -p1 -i ../$pkgname-$pkgver-shared.patch

  # add missing makefile and pkgconfig files
  cp ../{Makefile,*.pc} .

  # jack lib
  ./configure --prefix=/usr \
              --with-jack
  make jack

  # clean build
  make clean

  # alsa lib
  ./configure --prefix=/usr
  make alsa
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
