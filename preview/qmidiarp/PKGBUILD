# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=qmidiarp
pkgver=0.4.3
pkgrel=2
pkgdesc="A MIDI arpeggiator, phrase generator and controller LFO for the ALSA sequencer."
arch=(i686 x86_64)
url="http://sourceforge.net/projects/qmidiarp/"
license=('GPL')
depends=('qt' 'jack')
conflicts=("$pkgname-cvs")
install="$pkgname.install"
source=("http://switch.dl.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.tar.bz2"
        "http://switch.dl.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver-fix-transport.patch")
md5sums=('88511abc4eaa4bda6e11a48cfec97dcd'
         'feaf6bca8828aeb2a61c879c1ef3f161')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # official fix transport patch
  patch -p1 -i ../$pkgname-$pkgver-fix-transport.patch

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}
