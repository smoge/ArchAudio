# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=qjackrcd
pkgver=1.0.3
pkgrel=1
pkgdesc="A simple QT/JACK recorder"
arch=(i686 x86_64)
url="https://sourceforge.net/p/qjackrcd/home/Home/"
license=('GPL')
depends=('qt' 'jack')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/qjackrcd/sources/${pkgname}-src-$pkgver.tar.gz")
md5sums=('0f6a6f72fcdb406ab9f2e09e69c3d72a')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # build
  qmake
  make

  # translations
  lrelease $pkgname.pro
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # bin
  install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"

  # locales
  install -d "$pkgdir/usr/share/$pkgname/locale"
  install -Dm644 *.qm "$pkgdir/usr/share/$pkgname/locale"

  # desktop
  install -Dm644 $pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 record-red.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
