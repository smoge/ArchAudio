# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=qjackrcd
pkgver=1.0.2
pkgrel=1
pkgdesc="A simple QT/JACK recorder"
arch=(i686 x86_64)
url="https://sourceforge.net/p/qjackrcd/home/Home/"
license=('GPL')
depends=('qt' 'jack')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/qjackrcd/sources/${pkgname}-src-$pkgver.tar.gz"
        "$pkgname.desktop" "${pkgname}_it.ts")
md5sums=('de16dbcd86032eabb4f47fd8f6a74d65'
         '942ce33486d68a7e2cc7513f66a10199'
         '103f3aa03acaea4549b3ca6ba1807229')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # set path for locale
  sed -i 's|locale)|locale, "/usr/share/qjackrcd/locale")|' main.cpp

  # add translation
  cp ../${pkgname}_it.ts .
  sed -i 's/fr.ts$/& qjackrcd_it.ts/' $pkgname.pro

  # build
  qmake
  make

  # translations
  lrelease $pkgname.pro
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # bin
  install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"

  # locales
  install -d "$pkgdir/usr/share/$pkgname/locale"
  install -Dm644 *.qm "$pkgdir/usr/share/$pkgname/locale"

  # desktop
  install -Dm644 ../$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 record-red.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
