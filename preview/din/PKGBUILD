# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=din
pkgver=1.9.2
pkgrel=1
pkgdesc="A software musical instrument and audio synthesizer using Bezier curves to draw waveforms."
arch=(i686 x86_64)
url="http://dinisnoise.org/"
license=('GPL')
depends=('fftw' 'jack' 'liblo' 'libgl' 'libircclient' 'tcl')
install="$pkgname.install"
source=("http://din.googlecode.com/files/$pkgname-$pkgver.tar.gz"
        "$pkgname.desktop"
        "$pkgname.png")
md5sums=('1e98e4f076cef583d9224c6c2ad0996f'
         '5c0492262c13b4d6393c176c057f0c8f'
         'a13fda9d0c9640197aa7d5dd6a14a8d4')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # set right share path
  sed -i "s|var/|share/|" data/checkdotdin

  ./configure --prefix=/usr \
              --localstatedir=/usr/share

  make CXXFLAGS="$CXXFLAGS -lrt -lX11"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # desktop file
  install -Dm644 ../$pkgname.desktop \
          "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 ../$pkgname.png \
          "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
