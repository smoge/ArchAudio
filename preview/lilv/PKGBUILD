# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=lilv
pkgver=0.4.4
pkgrel=2
pkgdesc="A library to make the use of LV2 plugins as simple as possible for applications."
arch=(i686 x86_64)
url="http://drobilla.net/software/$pkgname/"
license=('custom:ISC')
depends=('python2' 'sord' 'jack')
makedepends=('lv2core' 'swig' 'lv2-event' 'lv2-uri-map' 'lv2-dyn-manifest')
optdepends=('bash-completion: auto-complete words')
conflicts=("$pkgname-svn")
source=("http://download.drobilla.net/$pkgname-$pkgver.tar.bz2"
        "http://downloads.sourceforge.net/project/naspro/naspro/0.2.91/lilv-0.4.2-dyn-manifest-fix.patch")
md5sums=('aa247f320e4022044920bb753e67ec6c'
         'b422fa00dd7abb3be4fc49ed6e8308b2')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export PYTHON="/usr/bin/python2"

  # remove ldconfig
  sed -i "/ldconfig/d" wscript

  # naspro patch
  patch -p0 -i ../lilv-0.4.2-dyn-manifest-fix.patch

  python2 ./waf configure --prefix=/usr \
                          --mandir=/usr/share/man \
                          --dyn-manifest \
                          --bindings
  python2 ./waf
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  DESTDIR="$pkgdir" python2 ./waf install

  # license
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
