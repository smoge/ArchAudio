# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: cga <cga@cga.cx>

_pkg=IanniX
pkgname=iannix
pkgver=0.8.21
pkgrel=2
pkgdesc="A score editor based on the UPIC sequencer by Iannis Xenakis."
arch=('i686' 'x86_64')
url="http://www.iannix.org/"
license=('GPL')
depends=('qt' 'shared-mime-info')
makedepends=('automoc4')
conflicts=("$pkgname-svn")
install=("$pkgname.install")
options=('!emptydirs')
source=("${url}download/${pkgname}_sources__${pkgver//./_}.zip"
        "x-$pkgname.desktop"
        "$pkgname.desktop"
        "$pkgname-mime.xml"
        "$pkgname.png")
md5sums=('3c38d117751c29bffa5d4d16f301a847'
         'fced40a56f922f47581962e34864bb96'
         '80a4af3e4800d3c837d11d3d33e9f56e'
         'b2e86f51136ce7c923cb4fbae9e7e5a5'
         '6441847848fd185ad2ba2a1dfeb502c4')

build() {
  cd "$srcdir/$_pkg"

  # let iannix fetch datas from /usr/share/iannix
  sed -i "s|\./|/usr/share/$pkgname/|g" $pkgname.cpp Tools/*
  sed -i "s_\"\(Tools\|Examples\|Project\|Patches\|Documentation\)_\"/usr/share/$pkgname/\1_g" \
    $pkgname.cpp extoscpatternask.h uiview.h

  qmake $_pkg.pro
  make
}

package() {
  cd "$srcdir/$_pkg"

  # bin
  install -Dm755 $_pkg "$pkgdir/usr/bin/$pkgname"

  # data
  install -d "$pkgdir/usr/share/$pkgname"
  cp -a Examples Tools Patches Project Documentation \
    "$pkgdir/usr/share/$pkgname"
  rm -rf "$pkgdir/usr/share/$pkgname/Patches/Ableton"*

  # dekstop
  install -Dm644 ../$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 ../$pkgname.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"

  # mime
  install -Dm644 ../x-$pkgname.desktop \
    "$pkgdir/usr/share/applications"
  install -Dm644 ../$pkgname-mime.xml \
    "$pkgdir/usr/share/mime/applications/$pkgname-mime.xml"

  # remove svn files
  rm -rf `find "$pkgdir" -type d -name ".svn"`
}
