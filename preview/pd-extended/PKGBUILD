# Maintainer: Arch Linux Pro Audio <dev@archaudio.org>
# Contributor:  Anh Hai Trinh <anh.hai.trinh@gmail.com>
# Contributor: dmotd <inaudible@simplesuperlativ.es>

pkgname=pd-extended
pkgver=0.42.5
pkgrel=5
provides=('pd-extended')
pkgdesc="PureData: a real-time music and multimedia environment"
url="http://puredata.info/"
arch=('i686' 'x86_64')
license=('BSD')
depends=('libv4l' 'fftw' 'jack' 'tk' 'freeglut' \
  'libquicktime' 'libdv' 'gsl' 'imagemagick' \
  'ftgl' 'libgl' 'dssi')
makedepends=('swig' 'automake' 'curl')
conflicts=()
provides=('pd-extended') # 'pd' 'pd-gem=0.91.3' 'pdp' 'zexy'
replaces=('pd-extended-svn')
backup=()
options=('!makeflags')
install=
source=(http://downloads.sourceforge.net/pure-data/Pd-0.42.5-extended.tar.gz)
md5sums=(54c20a51d4d07396f6a806ab46b31b18)

_realsrcdir=$srcdir/Pd-$pkgver-extended

build() {
  unset CFLAGS
  unset LDFLAGS
  unset INCLUDES

  cd $_realsrcdir

  # skip fetching doc, server didn't response at all for me
  sed -e 's/curl --connect-timeout 600 --max-time 3600/false/' -i doc/Makefile

  if [ "$CARCH" = "x86_64" ]; then
  # fix -fPIC issue in PDP
    sed -e "s|CFLAGS =|CFLAGS = -fPIC|" \
      -i externals/pdp/opengl/Makefile.config
  # fix -fPIC issue in pddp
    sed -e "s|DEFINES =|DEFINES = -fPIC|" \
      -i externals/miXed/Makefile.common
  # setting additional variable
    FPIC_FLAG="-fPIC"
    else FPIC_FLAG=""
  fi

  # build and install
  cd $_realsrcdir/packages/linux_make
  make \
    -C $_realsrcdir/packages \
    GEM_EXTRA_CXXFLAGS="$FPIC_FLAG" \
    BUILDLAYOUT_DIR=$_realsrcdir/packages \
    DESTDIR=$pkgdir \
    prefix=/usr \
    install
}

package () {
  cd $_realsrcdir

  install -Dm644 pd/LICENSE.txt \
    $pkgdir/usr/share/licenses/pd-extended-$pkgver/LICENSE.txt

  cd packages/
  mkdir -p $pkgdir/usr/lib/pd-extended
  install -p linux_make/default.pdextended $pkgdir/usr/lib/pd-extended

  # Gnome menu support
  install -d $pkgdir/usr/share/icons/hicolor/128x128/apps
  install -p -m0644 linux_make/pd.png \
    $pkgdir/usr/share/icons/hicolor/128x128/apps/
  install -d $pkgdir/usr/share/icons/hicolor/48x48/apps
  install -p -m0644 linux_make/pd-48x48.png \
    $pkgdir/usr/share/icons/hicolor/48x48/apps/pd.png
  install -d $pkgdir/usr/share/applications/
  install -p linux_make/pd-extended.desktop \
    $pkgdir/usr/share/applications/

  # files for /etc
  cd $_realsrcdir
  install -d $pkgdir/etc/bash_completion.d/
  install -p scripts/bash_completion/pd $pkgdir/etc/bash_completion.d

  # Pd-related scripts
  mkdir -p $pkgdir/usr/bin
  install -p scripts/pd-diff $pkgdir/usr/bin/
  install -p scripts/config-switcher.sh $pkgdir/usr/bin/
}
