# Maintainer : Ray Rashif <schivmeister@gmail.com>
# Contributor: SpepS <dreamspepser at yahoo dot it>

pkgname=rakarrack
pkgver=0.6.1
pkgrel=2
pkgdesc="Versatile guitar multi-effects processor."
arch=('i686' 'x86_64')
license=('GPL')
url="http://rakarrack.sourceforge.net"
depends=('alsa-utils' 'jack' 'fltk' 'libxpm')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2"
        'fltk_include.patch')
md5sums=('56b1e04779ae3d56cc8a3ad3c4e25152'
         'd43512466f514434daaf16bd72951ea0')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # fix for latest fltk include dir name
  patch -Np1 -i "$srcdir/fltk_include.patch"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
