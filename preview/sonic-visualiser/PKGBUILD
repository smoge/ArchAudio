# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Orivej Desh <masecretaire@gmx.fr>

pkgname=sonic-visualiser
pkgver=1.8
pkgrel=3
pkgdesc="A viewer and analyser of music audio files."
arch=('i686' 'x86_64')
url="http://www.sonicvisualiser.org/"
license=('GPL2')
depends=('qt' 'liblrdf' 'bzip2' 'rubberband' 'liblo' 'libid3tag' 'redland'
         'libmad' 'libfishsound' 'libpulse' 'liboggz' 'portaudio' 'jack')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/sv1/$pkgname-$pkgver.tar.gz")
md5sums=('ad676d0b4cf5b8561306487f52642862')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # include missing header
  sed -i "20a#include <stdlib.h>" svcore/base/ResizeableBitset.h

  # do not check for raptor1
  sed -i "/raptor/d" `find . -name configure.ac`

  # regenerate configure scripts
  sed -i "s|\(\" \&\& \)\(./configure\)|\1autoreconf \&\& \2|" configure

  # build against Qt4
  export QTDIR=/usr

  ./configure

  # Remove flac cflags (-I/usr/include/FLAC) to
  # prevent g++ fetching assert.h from flac headers
  sed -i "s|/FLAC||g" `grep -rl /FLAC . 2> /dev/null`

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/$pkgname"

  # bin
  install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"

  # desktop file and pixmaps
  install -d "$pkgdir/usr/share/applications"
  install -Dm644 *.desktop "$pkgdir/usr/share/applications"

  for _s in 16 22 24 32 48 64; do
    install -Dm644 icons/sv-${_s}x${_s}.png \
            "$pkgdir/usr/share/icons/hicolor/${_s}x${_s}/apps/sv-icon.png"
  done

  install -Dm644 icons/sv-icon.svg \
          "$pkgdir/usr/share/icons/hicolor/scalable/apps/sv-icon.svg"

  # samples
  install -d "$pkgdir/usr/share/$pkgname/samples"
  install -Dm644 samples/* "$pkgdir/usr/share/$pkgname/samples"
}
