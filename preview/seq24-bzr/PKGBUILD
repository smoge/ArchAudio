# Contributor: Sean Bartell <wingedtachikoma@gmail.com>
pkgname=seq24-bzr
pkgver=125
pkgrel=1
pkgdesc="A pattern-based midi sequencer with strong live performance functions"
arch=('i686' 'x86_64')
url="http://launchpad.net/seq24"
license=('GPL')
depends=('gtkmm>=2.4.0' 'lash>=0.5.0')
makedepends=('bzr')
provides=('seq24')
conflicts=('seq24')

_bzrtrunk="http://bazaar.launchpad.net/%7Eseq24team/seq24/trunk"
_bzrmod="seq24"

build() {
  cd "$srcdir"

  if [ -d "$_bzrmod/.bzr" ]; then
    (cd "$_bzrmod" && bzr merge -r "$pkgver")
  else
    bzr branch "$_bzrtrunk" -r "$pkgver" "$_bzrmod"
  fi

  msg "Bazaar merge done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_bzrmod-build"
  cp -r "$srcdir/$_bzrmod" "$srcdir/$_bzrmod-build"
  cd "$srcdir/$_bzrmod-build"

  autoreconf --install
  ./configure --prefix=/usr --enable-lash
  make
  make DESTDIR="$pkgdir/" install
}
