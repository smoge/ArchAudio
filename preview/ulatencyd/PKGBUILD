# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=ulatencyd
pkgver=0.5.0
pkgrel=1
pkgdesc="Daemon to minimize latency on a linux system using cgroups"
arch=(i686 x86_64)
url="https://github.com/poelzi/ulatencyd"
license=('GPL')
depends=('lua' 'luaposix' 'dbus-glib' 'libxcb' 'polkit')
optdepends=('python2-qt: qt gui'
            'systemd: systemd service')
makedepends=('cmake')
conflicts=("$pkgname-git")
backup=("etc/$pkgname/$pkgname.conf")
options=('!emptydirs')
source=("https://github.com/downloads/poelzi/$pkgname/$pkgname-$pkgver.tar.gz"
        "$pkgname.d"
        "$pkgname")
md5sums=('ef03b9c62ebc2f24327963e0c3a55eae'
         'ceb27033cb79d2a9ad71e390b0b75d7d'
         '512bd9215650c95c04717abf8a2d2f40')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Python 2 fix for pyqt gui
  sed -i "s|\(env python\).*|\12|" client/ulatency

  # man path fix
  sed -i "s|/man/man|/share&|g" docs/CMakeLists.txt

  # closing STDOUT in daemon mode cause 100% CPU
  # quick fix: remove close(STDOUT_FILENO);
  # TODO: Signal this bug upstream
  sed -i "/close(STDOUT/d" src/$pkgname.c

  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # Daemon script
  install -Dm755 ../$pkgname "$pkgdir/etc/rc.d/$pkgname"
  install -Dm644 ../$pkgname.d "$pkgdir/etc/conf.d/$pkgname"
}

# vim:set ts=2 sw=2 et: