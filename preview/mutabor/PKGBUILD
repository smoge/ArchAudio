# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=mutabor
pkgver=3.1.0alpha2
pkgrel=1
pkgdesc="A Program that allows performing and composing microtonal music using a normal MIDI keybord."
arch=(i686 x86_64)
url="http://www.math.tu-dresden.de/~mutabor/"
license=('GPL')
depends=('alsa-lib' 'wxgtk')
makedepends=('perl')
options=('!libtool')
install="$pkgname.install"
source=("http://download.berlios.de/$pkgname/$pkgname-$pkgver.tar.gz"
        "$pkgname.desktop")
md5sums=('083b4973557be087fb2513638dea5f95'
         '3846971fe53cfa052267e38c472b6e0e')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr \
              --libdir=/usr/lib/$pkgname \
              --enable-static=no
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # icon and desktop file
  install -Dm644 ../$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  install -Dm644 Images/about.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
