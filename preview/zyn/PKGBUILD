# Maintainer: SpepS <dreamspepser at yahoo dot it>
# Contributor: Nate Slottow (m00tp01nt) <nslottow AT gmail DOT com>

pkgname=zyn
pkgver=1
pkgrel=4
pkgdesc="The zyn project main goal is to extract synth engines from ZynAddSubFX and pack them in LV2 plugin format."
arch=('i686' 'x86_64')
url="http://home.gna.org/zyn/"
license=('GPL')
depends=('lv2core' 'lv2dynparam1' 'fftw-threads')
source=(http://download.gna.org/zyn/$pkgname-$pkgver.tar.bz2)
md5sums=('ead0a8db5440e480c8a23441195a76df')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Namespace Fix
  sed "s:\({ int\):_zyn_filter_processor_handle \1:" -i filter_common.h

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  mkdir -p "$pkgdir/usr/lib/lv2"
  make LV2_INSTALL_PATH="$pkgdir/usr/lib/lv2" install
}