# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkg=guitarix
pkgname=${_pkg}2
pkgver=0.19.0
pkgrel=1
pkgdesc="A simple mono amplifier to be used in a 'JACKified' environment."
arch=('i686' 'x86_64')
url="http://sourceforge.net/apps/wordpress/guitarix/"
license=('GPL')
depends=('gtkmm' 'jack' 'fftw')
makedepends=('python2' 'boost' 'intltool')
optdepends=('meterbridge: sound meters')
conflicts=("$_pkg" 'gx_head')
replaces=("$_pkg" 'gx_head')
install="$pkgname.install"
options=('!strip')
source=("http://switch.dl.sourceforge.net/project/$_pkg/$_pkg/$pkgname-$pkgver.tar.bz2")
md5sums=('6b103e306225577a479a601c4eff5a30')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fixes
  sed -i "s|\(^#!.*bin/python\).*|\12|;s|\(^#!.*env python\).*|\12|;" \
    `grep -Erl "(env python|bin/python)" .`

  python2 waf configure --prefix=/usr \
                        --includeresampler \
                        --includeconvolver \
                        --no-faust \
                        --optimization
  python2 waf build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf install --destdir="$pkgdir"
}
