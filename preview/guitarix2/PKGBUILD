# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkg=guitarix
pkgname=${_pkg}2
pkgver=0.18.0
pkgrel=2
pkgdesc="A simple mono amplifier to be used in a 'JACKified' environment."
arch=('i686' 'x86_64')
url="http://sourceforge.net/apps/wordpress/guitarix/"
license=('GPL')
depends=('gtkmm' 'jack' 'zita-convolver')
makedepends=('python2' 'boost')
optdepends=('meterbridge: sound meters')
conflicts=("$_pkg" 'gx_head')
replaces=("$_pkg" 'gx_head')
install="$pkgname.install"
source=("http://switch.dl.sourceforge.net/project/$_pkg/$_pkg/$pkgname-$pkgver.tar.bz2")
md5sums=('4dd748d2607c5d4a66592c3571c52390')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fixes
  sed -i "s|\(^#!.*bin/python\).*|\12|;s|\(^#!.*env python\).*|\12|;" \
    `grep -Erl "(env python|bin/python)" .`

  # remove ldconfig in wscript
  sed -i "598,599d" wscript

  python2 waf configure --prefix=/usr \
                        --includeresampler \
                        --no-faust \
                        --optimization

  python2 waf build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf install --destdir="$pkgdir"
}
