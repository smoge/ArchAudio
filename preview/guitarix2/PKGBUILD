# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkg=guitarix
pkgname=${_pkg}2
pkgver=0.17.0
pkgrel=1
pkgdesc="A simple mono amplifier to be used in a 'JACKified' environment."
arch=('i686' 'x86_64')
url="http://sourceforge.net/apps/wordpress/guitarix/"
license=('GPL')
depends=('gtkmm' 'jack' 'zita-resampler' 'zita-convolver')
makedepends=('python2' 'boost')
conflicts=("$_pkg" 'gx_head')
replaces=("$_pkg" 'gx_head')
install="$pkgname.install"
source=("http://switch.dl.sourceforge.net/project/$_pkg/$_pkg/$pkgname-$pkgver.tar.bz2")
md5sums=('e915d5f2f4fcdda0f9614678d04d9bd1')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fixes
  sed -i "s|\(^#!.*bin/python\).*|\12|;s|\(^#!.*env python\).*|\12|;" \
    `grep -Erl "(env python|bin/python)" .`

  # python2 fixes for python wrapper (do not build actually)
  sed -i "s|\(\"python\) |\12 |g" {pygxw,libgxwmm/gxwmm}/wscript

  # remove ldconfig in wscript
  sed -i "597,598d" wscript

  python2 waf configure --prefix=/usr \
                        --shared-lib \
                        --glade-support
#                        --generate-cpp-wrapper \
#                        --python-wrapper
  python2 waf build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf install --destdir="$pkgdir"
}
