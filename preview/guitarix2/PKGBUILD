# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkg=guitarix
pkgname=${_pkg}2
pkgver=0.18.0
pkgrel=1
pkgdesc="A simple mono amplifier to be used in a 'JACKified' environment."
arch=('i686' 'x86_64')
url="http://sourceforge.net/apps/wordpress/guitarix/"
license=('GPL')
depends=('gtkmm' 'jack' 'zita-resampler' 'zita-convolver')
makedepends=('python2' 'boost' 'faust>=0.9.43')
conflicts=("$_pkg" 'gx_head')
replaces=("$_pkg" 'gx_head')
install="$pkgname.install"
source=("http://switch.dl.sourceforge.net/project/$_pkg/$_pkg/$pkgname-$pkgver.tar.bz2")
md5sums=('4dd748d2607c5d4a66592c3571c52390')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fixes
  sed -i "s|\(^#!.*bin/python\).*|\12|;s|\(^#!.*env python\).*|\12|;" \
    `grep -Erl "(env python|bin/python)" .`

  # python2 fixes for python wrapper (do not build actually)
  sed -i "s|\(\"python\) |\12 |g" {pygxw,libgxwmm/gxwmm}/wscript

  # remove ldconfig in wscript
  sed -i "598,599d" wscript

  # add support to latest faust 0.9.43
  sed -i "s|\(0.9.36'\)]|\1,'0.9.43']|" wscript
  sed -i "s|osc\(\.lib\)|oscillator\1|g" `grep -rl "osc\.lib" .`

  python2 waf configure --prefix=/usr \
                        --shared-lib \
                        --glade-support
#                        --python-wrapper
#                        --generate-cpp-wrapper
  python2 waf build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf install --destdir="$pkgdir"
}
