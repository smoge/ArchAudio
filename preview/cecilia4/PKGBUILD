# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=cecilia4
pkgver=4.2_beta
pkgrel=1
pkgdesc="An audio signal processing environment using Csound as its audio engine. Python rewrite."
arch=('any')
url="http://code.google.com/p/cecilia4/"
license=('GPL')
depends=('csound' 'pyaudio' 'wxpython' 'python-pyo>=0.03')
install="$pkgname.install"
source=("http://cecilia4.googlecode.com/files/Cecilia_$pkgver-src.tar.bz2"
        "$pkgname.desktop"
        "${url}logo")
md5sums=('a0c1fc7a3bec3f75d0a8674cf164e062'
         'f3f3896e4706562f41c5900b1a3f616d'
         '85b54c95a8b7ff6eb477f83f81b465d8')

build() {
  cd "$srcdir/Cecilia_$pkgver-src"

  sed -i "s|\(env python\).*|\12|" `grep -Erl "env python" .`
}

package() {
  cd "$srcdir/Cecilia_$pkgver-src"

  install -d "$pkgdir"/usr/{bin,share/{applications,pixmaps,$pkgname}}

  # Bin
  cat << EOF >> "$pkgdir/usr/bin/$pkgname"
#!/bin/bash
cd /usr/share/$pkgname && python2 Cecilia.py
EOF
  chmod 755 "$pkgdir/usr/bin/$pkgname"

  # Data files
  cp -a Cecilia.py doc Resources tests "$pkgdir/usr/share/$pkgname"

  # Desktop file and pixmaps
  install -Dm 644 ../$pkgname.desktop "$pkgdir/usr/share/applications"
  install -Dm 644 ../logo "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
