# Maintainer: SpepS <dreamspepser at yahoo dot it>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>
# Contributor: damir <damir@archlinux.org>

pkgname=gtick
pkgver=0.5.0
pkgrel=1
pkgdesc="A Gtk metronome application supporting different meters and speeds."
arch=('i686' 'x86_64')
url="http://www.antcom.de/gtick/"
license=('GPL3')
depends=('gtk2' 'libpulse')
makedepends=('libsndfile')
optdepends=('pulseaudio: pulseaudio sound system support')
install="$pkgname.install"
source=("http://www.antcom.de/gtick/download/$pkgname-$pkgver.tar.gz"
        "http://www.antcom.de/gtick/mcenaney.png"
	"$pkgname.desktop")
md5sums=('60a3e11000ec4b7a333cd0957b9cc841'
         '01f8a09c9f9486df868963c67adecf73'
         '862cbb21d3ddbd0cdbcc9afdc141414f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # use oss as default sound system
  sed -i "s|pulseaudio|oss|" src/globals.h
  sed -i "/soundsystem =/s/pulseaudio/oss/" src/gtkoptions.c

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # desktop file
  install -Dm644 ../$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 ../mcenaney.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
