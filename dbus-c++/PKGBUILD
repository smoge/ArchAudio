# Contributor: Zarra <zarraxx@gmail.com>
# Contributor: Bernardo Barros <bernardobarros@NOSPAM.gmail.com>

pkgname=dbus-c++
pkgver=20110530
pkgrel=1
pkgdesc="dbus-c++ attempts to provide a C++ API for D-BUS. "
url="http://freedesktop.org/wiki/Software/dbus-c++"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus>=1.2.0' 'glib2')
makedepends=('git' 'autoconf' 'automake')
source=(gcc44.patch Makefile.patch)
options=('!libtool')

md5sums=('55a7d473b890683e2b8aeb26c234853f'
         '09862e6dd29f0cc34f1537130c1e19cc')
         
#_gitroot="git://anongit.freedesktop.org/git/dbus/dbus-c++/"
_gitroot="git://gitorious.org/dbus-cplusplus/mainline.git"
_gitname="dbus-c++"


build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  patch -Np1 -i "$srcdir"/gcc44.patch 

  ./autogen.sh --prefix=/usr --disable-static --enable-glib

  patch -Np0 -i "$srcdir"/Makefile.patch 

  make
}

package() {
  cd "$srcdir/$_gitname-build"

  make DESTDIR="$pkgdir" install
}

