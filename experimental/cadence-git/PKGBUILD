# Maintainer: SpepS <dreamspepser at yahoo dot it>

_p=cadence
pkgname=$_p-git
pkgver=20110803
pkgrel=1
pkgdesc="A set of tools to handle Jack, useful for audio production."
arch=(i686 x86_64)
url="http://sourceforge.net/projects/cadence/"
license=('GPL')
depends=('python2-pyqt' 'dbus-python' 'gtk2' 'liblo' 'jackdbus')
makedepends=('git')
optdepends=('pulseaudio: pulseaudio support'
            'a2jmidid: a2jmidid support'
            'jack_capture: jack caputure support'
            'pyliblo: osc support for Carla Control'
            'rdflib: rdf support for Carla'
            'ladish: ladish support for Claudia'
            'pyxdg: xdg support for Claudia'
            'xdg-utils: xdg support for Claudia')
provides=("$_p")
conflicts=("$_p")
options=('!emptydirs')
install="$pkgname.install"

_gitroot="git://repo.or.cz/$_p.git"
_gitname="$_p"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #

  # python2 fixes
  sed -i "s|\(env python\).*|\12|" `grep -rl "env python" .`

  # look for carla_backend.so in /usr/lib
  sed -i "s|cwd.*carla\"|'/usr/lib'|" src/carla_backend.py

  make PREFIX=/usr
}

package() {
  cd "$srcdir/$_gitname-build"

  make PREFIX=/usr INSTALL_PREFIX="\/usr" DESTDIR="$pkgdir/" install

  # python2 fixes
  sed -i "s|\(python\)\(\`*\)[^ ]*|\12\2|" "$pkgdir/usr/bin/"*

  # move lib carla backend lib to proper path
  install -d "$pkgdir/usr/lib"
  mv "$pkgdir/usr/share/$_p/src/carla/carla_backend.so" \
     "$pkgdir/usr/lib"

  # strip any local reference
  sed -i "s|/local||" `grep -rl /local $pkgdir`
} 
