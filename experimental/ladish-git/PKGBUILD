# Maintainer: SpepS <dreamspepser at yahoo dot it>

_p=ladish
pkgname=$_p-git
pkgver=20110801
pkgrel=1
pkgdesc="A session management system for JACK applications on GNU/Linux."
arch=(i686 x86_64)
url="http://ladish.org/"
license=('GPL')
depends=('jackdbus' 'dbus-glib' 'laditools-git' 'flowcanvas<=0.7.1' 'a2jmidid')
makedepends=('git' 'python2')
provides=("$_p=0.4" 'lash')
conflicts=("$_p" 'lash')
install="$pkgname.install"

_gitroot="git://repo.or.cz/$_p.git"
_gitname="$_p"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #

  # python2 fix
  export PYTHON=/usr/bin/python2
  sed -i "s|env python|&2|" ${_p}_control

  # build release
  sed -i "s|\(RELEASE = \).*|\1True|" wscript

  python2 waf configure --prefix=/usr \
                        --enable-liblash \
                        --enable-pylash
  python2 waf
}

package() {
  cd "$srcdir/$_gitname-build"

  python2 waf install --destdir="$pkgdir/"
} 
