# Maintainer : bjoern lindig (bjoern _dot_ lindig _at_ google.com)
# Contributor: SpepS <dreamspepser at yahoo dot it>

_pkg=faust2
pkgname=faust2-git
pkgver=20110520
pkgrel=1
pkgdesc="Faust AUdio STreams is a functional programming language for realtime audio signal processing."
arch=('i686' 'x86_64')
url="http://faust.grame.fr/"
license=('GPL')
depends=('coreutils' 'libffi')
makedepends=('git')
provides=('faust2') # do not want to conflict with faust for now

_gitroot="git://faudiostream.git.sourceforge.net/gitroot/faudiostream/faudiostream"
_gitname="$_pkg"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
    cd $_gitname && git checkout -b $_pkg origin/$_pkg
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"

  cd "$srcdir/$_gitname-build"
  #
  # BUILD HERE
  #

  # libs in /usr/lib/faust2 to prevent conflicts with faust
  sed -i "s|/lib/faust|&2|g" Makefile

  # fix llvm path
  sed -i "s|/System/|/Support/|g" compiler/Makefile.unix

  # strip any reference to /usr/local
  sed -i "s|/local||g" `grep -rl /local .`

  # replace faust with faust2
  sed -i "s|\(faust\)\([^2]\{1,1\}\)|\12\2|g" tools/faust2appls/faust2*

  make PREFIX=/usr
}

package() {
  cd "$srcdir/$_gitname-build"

  make PREFIX=/usr/ DESTDIR="$pkgdir/" install

  # tools
  cd tools/faust2appls && make PREFIX=/usr DESTDIR="$pkgdir/" install

  # examples
  install -d "$pkgdir/usr/share/$_pkg"
  cp -a ../../examples "$pkgdir/usr/share/$_pkg"

  # faust is now faust2
  mv "$pkgdir/usr/bin/faust" "$pkgdir/usr/bin/$_pkg"
}
