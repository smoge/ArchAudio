# Contributor: Ray Rashif <schivmeister@gmail.com>
# Contributor: Jon Kristian Nilsen <jokr.nilsen@gmail.com>
# Contributor: Bernardo Barros <bernardobarros@NOSPAM.gmail.com>

pkgname=libffado-svn
pkgver=1985
pkgrel=1
pkgdesc="Driver for FireWire audio devices (development version)"
arch=('i686' 'x86_64')
url="http://www.ffado.org/"
license=('GPL')
depends=('libiec61883' 'libavc1394' 'libxml++' 'dbus' 'alsa-lib' 'libsigc++' 'jack')
makedepends=('scons' 'python2-qt' 'subversion' 'libconfig' 'dbus-c++')
optdepends=('python2-qt: FFADO Mixer applet'
            'libconfig: ffado-test')
provides=('ffado' 'libffado')
conflicts=('ffado' 'ffado-svn' 'libffado')
replaces=('libffado')
options=('!libtool' '!strip')

_svntrunk=http://subversion.ffado.org/ffado/trunk/libffado
_svnmod=ffado

# Debug: 'yes' or 'no'
_DEBUG=yes

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

 # hack to use CFLAGS without optimising
  sed -i 's/-O2//g' SConstruct

  if [ "$CARCH" = "x86_64" ]; then
    sed -i "s/-m64/-m64 $CFLAGS/g" SConstruct
  else
    sed -i "s/-m32/-m32 $CFLAGS/g" SConstruct
  fi

  # python2 fix
  sed -i 's:python $SOURCE:python2 $SOURCE:' SConstruct
  sed -i 's:python support/tools:python2 support/tools:' \
    support/tools/SConscript


  if [[ "$_DEBUG" = "no" ]]; then
    msg "Doing a optimized build"
    scons PREFIX=/usr DEBUG=no ENABLE_OPTIMIZATIONS=yes
  elif [[ "$_DEBUG" = "yes" ]]; then
    msg "Doing a debug build"
    scons PREFIX=/usr DEBUG=yes ENABLE_OPTIMIZATIONS=no
    
  fi
}

package() {
  cd "$srcdir/$_svnmod-build"

  scons DESTDIR="$pkgdir/" WILL_DEAL_WITH_XDG_MYSELF="True" install

  # python2 fix
  for i in $(grep '^#!.*bin.*python' -R "$pkgdir" | sed 's/:.*//'); do
    sed -i 's:^#!.*bin.*python:#!/usr/bin/python2:' "$i"
  done

  # desktop files & icons
  install -Dm644 support/xdg/ffado.org-ffadomixer.desktop \
    "$pkgdir/usr/share/applications/ffadomixer.desktop"

  install -Dm644 support/xdg/hi64-apps-ffado.png \
        "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
