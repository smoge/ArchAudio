# Maintainer: Patrick McCarty <pnorcks at gmail dot com>

pkgname=lilypond-git
pkgver=20110507
pkgrel=1
pkgdesc="An automated music engraving system (Git snapshot)"
arch=('i686' 'x86_64')
url="http://lilypond.org/"
license=('GPL')
depends=('fontconfig' 'freetype2' 'ghostscript'
         'guile' 'pango' 'python2')
makedepends=('bison' 'flex' 'fontforge' 'gettext' 'git' 'gsfonts'
             'perl' 'texinfo' 't1utils' 'texlive-core')
optdepends=('netpbm: building HTML documentation'
            'imagemagick: building HTML documentation'
            'ttf-kochi-substitute: building HTML documentation'
            'texi2html>=1.82: building HTML documentation'
            'rsync: installing HTML documentation')
provides=('lilypond')
conflicts=('lilypond' 'lilypond-devel')
install=$pkgname.install
source=(python2.patch texlive-workaround.patch)
sha256sums=('80f5be4313d08bd481d0dd04e767ebb79d1b99b05fb373b5d4cb96cdbb5cf4b4'
            'b0ec3b9b2e6f9a4ed7d4a40f8b0d86de565e37d513d6a6e02c556b59931124af')

_gitroot="git://git.sv.gnu.org/lilypond.git"
_gitname="lilypond"

build() {
  cd "$srcdir"
  msg "Connecting to git.sv.gnu.org..."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone --depth 1 $_gitroot
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  cp -R "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  # python2 fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  patch -p1 -i "$srcdir/python2.patch"
  patch -p1 -i "$srcdir/texlive-workaround.patch"

  PYTHON="python2" ./autogen.sh --prefix=/usr
  make all
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
