# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkg=buzztard
pkgname=$_pkg-svn
pkgver=3320
pkgrel=1
pkgdesc="A modular, free, open source music studio that is conceptually based on Buzz."
arch=('i686' 'x86_64')
url="http://www.buzztard.org"
license=('LGPL')
depends=('gst-buzztard-svn' 'libgsf' 'libgnomecanvas' 'gstreamer0.10-base' 'gconf'
         'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('subversion')
optdepends=("hal: deprecated hal support")
provides=("$_pkg")
conflicts=("$_pkg")
options=('!libtool' '!emptydirs')
install="$pkgname.install"
source=("$_pkg.desktop")
md5sums=('1939d6debfb4e0970d203fc8ab499059')

_svntrunk="https://buzztard.svn.sourceforge.net/svnroot/buzztard/trunk/$_pkg"
_svnmod="$_pkg"

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  #
  # BUILD
  #

  # prevent attempt to build and install
  # missing desktop file (buzztard-edit.desktop)
  sed -i "s|buzztard-edit.desktop[^ ]*||" src/ui/edit/Makefile*

  ./autogen.sh
  ./configure --prefix=/usr \
              --enable-static=no \
              --disable-introspection \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas

  make
}

package() {
  cd "$srcdir/$_svnmod-build"

  make DESTDIR="$pkgdir/" install

  # install missing desktop file
  install -Dm644 ../$_pkg.desktop "$pkgdir/usr/share/applications/$_pkg.desktop"

  # Deleting unneeded files
  rm -f "$pkgdir/usr/share/applications/mimeinfo.cache"
  find "$pkgdir/usr/share/mime" -maxdepth 1 -type f -exec rm {} \;
}
