# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Bernardo Barros <bernardobarros@gmail.com>
# Contributor: Ray Rashif <schivmeister@gmail.com>

_pkg=lv2core
pkgname=$_pkg-svn
pkgver=237
pkgrel=1
pkgdesc="A standard for plugins and matching host applications, mainly targeted at audio processing and generation."
arch=('any')
url="http://lv2plug.in/"
license=('GPL')
makedepends=('subversion' 'rdflib')
provides=("$_pkg" 'lv2-data-access' 'lv2-dyn-manifest' 'lv2-event'
          'lv2-instance-access' 'lv2-midi' 'lv2-presets' 'lv2-ui'
          'lv2-units' 'lv2-uri-map' 'lv2-atom' 'lv2-contexts'
          'lv2-host-info' 'lv2-osc' 'lv2-parameter' 'lv2-port-groups'
          'lv2-pui' 'lv2-pui-event' 'lv2-pui-gtk' 'lv2-resize-port'
          'lv2-string-port' 'lv2-uri-unmap')
conflicts=("$_pkg" 'lv2-data-access' 'lv2-dyn-manifest' 'lv2-event'
           'lv2-instance-access' 'lv2-midi' 'lv2-presets' 'lv2-ui'
           'lv2-units' 'lv2-uri-map' 'lv2-atom' 'lv2-contexts'
           'lv2-host-info' 'lv2-osc' 'lv2-parameter' 'lv2-port-groups'
           'lv2-pui' 'lv2-pui-event' 'lv2-pui-gtk' 'lv2-resize-port'
           'lv2-string-port' 'lv2-uri-unmap')

_svntrunk=http://lv2plug.in/repo/trunk
_svnmod=$_pkg

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  #
  # BUILD
  #

  python2 ./waf configure --prefix=/usr --mandir=/usr/share/man
  python2 waf
}

package() {
  cd "$srcdir/$_svnmod-build"

  python2 waf install --destdir="$pkgdir/"

  # generate includes manually
  for _ns in `find "$pkgdir/usr/lib/lv2" -name manifest.ttl \
              -exec grep "^<http" {} \; | sed "s|<http://\(.*\)>|\1|g"`; do
      _name=${_ns/*\/}
      _path="$pkgdir/usr/include/lv2/${_ns%/*}"
      [ -d "$_path" ] || install -d "$_path"
      msg "$_path/$_name => /usr/lib/lv2/$_name.lv2"
      ln -s "/usr/lib/lv2/$_name.lv2" "$_path/$_name"
  done

  # Delete lv2config
  rm -rf "$pkgdir/usr/bin"
}
