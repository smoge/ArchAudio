# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkg=oomidi
pkgname=$_pkg-git
pkgver=20110614
pkgrel=1
pkgdesc="Open Octave Midi, the open source MIDI/Audio sequencer."
arch=(i686 x86_64)
url="http://www.openoctave.org/"
license=('GPL')
depends=('qt' 'liblo' 'lash' 'fluidsynth' 'liblscp')
makedepends=('git' 'dssi')
provides=("$_pkg")
conflicts=("$_pkg" "oom")
install="$pkgname.install"

_gitroot="git://github.com/ccherrett/oom.git"
_gitname="$_pkg"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #

  # python2 fixes
  export PYTHON="python2"
  sed -i "s|bin/python|&2|;s|env python|&2|" \
    `grep -Erl "(bin/python|env python)" .`

  # adding missing header
  sed -i "14i#include <stddef.h>" oom/memory.h

  # prepare build folder
  mkdir build && cd build

  cmake .. -DCMAKE_BUILD_TYPE=release \
           -DCMAKE_INSTALL_PREFIX=/usr \
           -DENABLE_LASH=ON \
           -DENABLE_OSC=ON \
           -DENABLE_DSSI=ON \
           -DENABLE_FLUID=ON \
           -DENABLE_LSCP=ON
  make
}

package() {
  cd "$srcdir/$_gitname-build/build"

  make DESTDIR="$pkgdir/" install

  # prevent conflicts with muse 2
  cd "$pkgdir/usr/bin" && mv grepmidi oom-grepmidi
} 
