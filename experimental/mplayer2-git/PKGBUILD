# $Id$
# Maintainer: Arch Linux Pro Audio <dev@archaudio.org>
# Contributor: Bernardo Barros <bernardobarros@gmail.com>
# Contributor: cantabile <cantabile dot desu at gmail dot com>
# Contributor: extcake < extcake@gmail.com >

pkgname=mplayer2-git
pkgver=20110224
pkgrel=1
pkgdesc="A movie player for linux (uses statically linked ffmpeg-mt; git version)"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.mplayer2.org/"
depends=('fontconfig' 'libgl' 'libxss' 'libxxf86dga' 'sdl' 'ttf-dejavu')
makedepends=('git' 'live-media' 'mesa' 'python2' 'unzip' 'yasm')
backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
provides=('mplayer=31332')
conflicts=('mplayer')
source=('mplayer.desktop')
md5sums=('af6ba58cc34322bf12e46e8d1d42d75f')
install="mplayer2.install"

_gitroot="git://repo.or.cz/mplayer-build.git"
_gitname="mplayer2"

build() {
#   # Custom CFLAGS break the mplayer build
#   unset CFLAGS
#   unset LDFLAGS

  cd "${srcdir}"
  if [[ -d "${_gitname}" ]]; then
    cd "${_gitname}"
    git pull
  else
    git clone "${_gitroot}" "${_gitname}" && cd "${_gitname}"
  fi

  echo "
--prefix=/usr
--disable-fribidi
--disable-libdv
--enable-xvmc
--language=all
--confdir=/etc/mplayer
" > mplayer_options

#  for i in $(grep -r -l '/usr/bin/env python$' . 2>/dev/null); do sed -i 's:/usr/bin/env python$:&2:' "$i"; done
# for the paranoid, the following is supposed to handle filenames
# containing \n and other such abominations (thanks to grawity and others
# from irc)
  grep -r -lZ '/usr/bin/env python[^23]*' . 2>/dev/null | while read -rd '' file; do sed -i '1s:\<python\>:&2:' "$file"; done
  ./enable-mt
  ./init --shallow
  cd "${srcdir}"
  cp -r "${_gitname}" "${_gitname}-build"
  cd "${_gitname}-build"

  msg "Starting make..."

  make
}

package() {
  cd "${srcdir}/${_gitname}-build"

  make DESTDIR="${pkgdir}" install

  install -m644 mplayer/etc/{codecs,input,example}.conf "${pkgdir}/etc/mplayer/"
  install -d -m755 "${pkgdir}/usr/share/mplayer"
  ln -s /usr/share/fonts/TTF/DejaVuSans.ttf "${pkgdir}/usr/share/mplayer/subfont.ttf"

  # there is a desktop file where the xpm is, but we don't want that
  install -Dm644 "${srcdir}/mplayer.desktop" "${pkgdir}/usr/share/applications/mplayer.desktop"
  install -Dm644 "${srcdir}/${_gitname}/mplayer/etc/mplayer.xpm" "${pkgdir}/usr/share/pixmaps/mplayer.xpm"

  cd "${srcdir}"
  rm -rf "${_gitname}-build"
}
