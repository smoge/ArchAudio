# Contributor: Max Pray a.k.a. Synthead <synthead@gmail.com>
# Contributor: SpepS <dreamspepser at yahoo dot it>

_pkg=eugene
pkgname=$_pkg-svn
pkgver=3430
pkgrel=1
pkgdesc="A basic (fast, parallel) Genetic Algorithm implementation in C++"
arch=(i686 x86_64)
url="http://drobilla.net/software/$_pkg/"
license=('GPL')
depends=('flowcanvas>=1.0.0')
makedepends=('subversion' 'python2')
provides=("$_pkg=0.8.0")
conflicts=("$_pkg")

_svntrunk=http://svn.drobilla.net/lad/trunk/$_pkg
_svnmod=$_pkg

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  #
  # BUILD
  #

  # remove ldconfig
  sed -i "/ldconfig/d" wscript

  python2 waf configure --prefix=/usr
  python2 waf
}

package() {
  cd "$srcdir/$_svnmod-build"

  DESTDIR="$pkgdir/" python2 waf install
}
