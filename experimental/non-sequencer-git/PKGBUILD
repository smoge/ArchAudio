# Maintainer: orbisvicis <''.join(chr(ord(c)-1) for c in "pscjtwjdjtAhnbjm/dpn")>
# Contributor: Robert Schwarz <mail@rschwarz.net>
# Contributor: Lieven Moors <lievenmoors@gmail.com>

pkgname=non-sequencer-git
_pkg="${pkgname%-git}"
pkgver=20120307
pkgrel=1
pkgdesc="a powerful real-time, pattern-based MIDI sequencer"
arch=("i686" "x86_64")
url="http://${_pkg}.tuxfamily.org/"
license=("GPL2")
depends=("fltk>=1.1.7" "libsigc++2.0" "jack" "liblo" "libxpm")
makedepends=("git")
optdepends=()
provides=("$_pkg")
conflicts=("$_pkg")
options=()

_gitroot="git://git.tuxfamily.org/gitroot/non/sequencer.git"
_gitname="$_pkg"

build() {
  local _gitdir="${srcdir}/${_gitname}"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitdir" ]]; then
    cd "$_gitdir" &&
    git clean -dfx
    git reset --hard HEAD
    git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitdir"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  cd "$_gitdir"
  # Build
  ./configure --prefix=/usr
  make
}

package() {
  local _gitdir="${srcdir}/${_gitname}"
  cd "$_gitdir"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
