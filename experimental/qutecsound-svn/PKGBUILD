# Contributor: SpepS <dreamspepser at yahoo dot it>

pkgname=qutecsound-svn
_pkgname=qutecsound
pkgver=846
pkgrel=1
pkgdesc="A cross platform editor and front-end for Csound with syntax highlighting,"
arch=('i686' 'x86_64')
url="http://qutecsound.sourceforge.net/"
license=('GPL')
depends=('csound' 'qt')
makedepends=('subversion')
optdepends=('csound-doc: built-in help')
conflicts=('qutecsound')
provides=('qutecsound')
install="${_pkgname}.install"
source=("${_pkgname}.desktop")
md5sums=('061af6b0b3ac06b2f27f44871ec7f58b')

_svntrunk=https://qutecsound.svn.sourceforge.net/svnroot/qutecsound/trunk
_svnmod=qutecsound

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build/qutecsound"
  
  qmake qcs.pro
  make
}

package() {
  cd "$srcdir/$_svnmod-build/qutecsound"

  # Prepare folders
  install -d $pkgdir/usr/{bin,share/{applications,{,doc}/qutecsound}}

  # Bin file
  install -Dm755 bin/qutecsound-f "$pkgdir/usr/bin/${_pkgname}"

  # Examples docs and data
  cp -a examples $pkgdir/usr/share/$_pkgname
  cp -a images $pkgdir/usr/share/$_pkgname
  cp doc/* $pkgdir/usr/share/doc/$_pkgname

  # Desktop file and pixmaps
  install -Dm644 $srcdir/${_pkgname}.desktop "$pkgdir/usr/share/applications"
  install -Dm644 images/qtcs.png "$pkgdir/usr/share/pixmaps/${_pkgname}.png"
}

