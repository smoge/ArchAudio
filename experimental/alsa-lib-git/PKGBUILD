# Contributor: Bernardo Barros <bernardobarros@gmail.com>
# based on alsa-lib

pkgname=alsa-lib-git
pkgver=20110329
pkgrel=1
pkgdesc="An alternative implementation of Linux sound support - GIT version"
arch=('i686' 'x86_64')
url="http://www.alsa-project.org"
depends=('glibc')
makedepends=('python2' 'git')
optdepends=('python2')
license=('GPL')
options=(!libtool)
conflicts=('alsa-lib')
provides=('alsa-lib=1.0.23')

_gitroot="git://git.alsa-project.org/alsa-lib.git"
_gitname="alsa-lib"


build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"

  cd "$srcdir/$_gitname-build"

  libtoolize --force --copy --automake

  aclocal
  autoheader
  automake --foreign --copy --add-missing
  autoconf

  ./configure --prefix=/usr --with-pythonlibs="-lpthread -lm -ldl -lpython2.7" --with-pythonincludes=-I/usr/include/python2.7

  make
}

package() {

  cd "$srcdir/$_gitname-build"

  make DESTDIR="$pkgdir" install
}