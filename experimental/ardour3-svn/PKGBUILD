# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Bernardo Barros <bernardobarros@gmail.com>
# Contributor: Uli Armbruster <uli_armbruster@web.de>

_pkg=ardour3
pkgname=ardour3-svn
pkgver=9901
pkgrel=1
pkgdesc="A multichannel hard disk recorder and digital audio workstation"
arch=('i686' 'x86_64')
url="http://www.ardour.org/"
license=('GPL2')
depends=('libgnomecanvasmm>=2.12.0' 'jack>=0.118.0' 'liblrdf>=0.4.0' 'liblo'
         'aubio' 'slv2>=0.6.4' 'libsmf' 'lilv' 'suil' 'desktop-file-utils' 'soundtouch')
makedepends=('python2' 'boost>=1.39' 'cppunit>=1.12.0'  'doxygen' 'graphviz')
provides=("$_pkg")
conflicts=("$_pkg")
install=ardour3-svn.install
source=("${pkgname}.desktop")
md5sums=('2736c648e452ae45c703e48cf64f75e7')
        
_svntrunk="http://subversion.ardour.org/svn/ardour2/branches/3.0"
_svnmod="$_pkg"

build() {
  cd "$srcdir"

  if [ -d "${_svnmod}/.svn" ]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_svnmod}-build"
  cp -r "${srcdir}/${_svnmod}" "${srcdir}/${_svnmod}-build"
  cd "${srcdir}/${_svnmod}-build"

  #
  # BUILD
  #

  python2 waf configure \
    --prefix=/usr \
    --configdir=/etc \
    --docs \
    --freedesktop \
    --freesound \
    --lv2 \
    --nls \
    --tranzport \
    --noconfirm
  # enabled by default
  #   --debug

  python2 waf build
}

package() {
  cd "${srcdir}/${_svnmod}-build"

  python2 waf --destdir="${pkgdir}" install

  # desktop file
  install -Dm644 "${srcdir}/${pkgname}.desktop" \
    "${pkgdir}/usr/share/applications/${_pkg}.desktop"
}
