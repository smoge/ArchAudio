# $Id$
# Maintainer: Arch Linux Pro Audio <dev@archaudio.org>
# Contributor: Bernardo Barros <bernardobarros@gmail.com>

pkgname=qtractor-vst-svn
pkgver=2080
pkgrel=1
pkgdesc="Audio/MIDI multi-track sequencer application written in C++ around the Qt4 toolkit"
arch=('i686' 'x86_64')
url="http://qtractor.sourceforge.net"
license=('GPL')
depends=('qt' 'jack' 'slv2' 'libmad' 'libsamplerate' 'rubberband' 'liblo')
makedepends=('subversion' 'ladspa' 'dssi' 'steinberg-vst')
conflicts=('qtractor' 'qtractor-vst')

_svnmod='qtractor'
_svntrunk='https://qtractor.svn.sourceforge.net/svnroot/qtractor/trunk'

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  make -f Makefile.svn
  ./configure --prefix=/usr --with-vst=/usr/include/vst
  make
}

package() {  
  cd "$srcdir/$_svnmod-build"

  make DESTDIR="$pkgdir" install
}
