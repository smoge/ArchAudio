# Maintainer: Arch Linux Pro Audio <archaudio-devel@archaudio.org>
# Contributor: Bj√∂rn Lindig <bjoern.lindig@googlemail.com>
# Contributor: Bernardo Barros <bernardobarros@gmail.com>

# sced + gedit3 seems broken upstream
# sced may work with gedit2 

pkgname=supercollider-git
pkgver=20110731
pkgrel=1
pkgdesc="An environment and programming language for real time audio synthesis and algorithmic composition."
url="http://supercollider.sourceforge.net/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('jack' 'fftw' 'avahi' 'ruby' 'icu' 'cwiid' 'rsync')
makedepends=('git' 'libsndfile>=1.0' 'emacs' 'vim' 'gedit' 'pkgconfig>=0.14.0' 'cmake' 'alsa-lib' 'boost')
optdepends=('sc3-plugins-git: extra audio UGens'
            'swingosc: java based GUI system')
conflicts=('supercollider' 'supercute')
provides=('supercollider')

install=sc3.install

# Official git repo:
_gitroot="git://supercollider.git.sourceforge.net/gitroot/supercollider/supercollider"
_gitname="supercollider"

build() {

  export LDFLAGS="${LDFLAGS//-Wl,--as-needed}"

  cd $srcdir
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    (cd $_gitname && git pull origin && git submodule update)
    msg "The local files are updated."
  else
    git clone --recursive $_gitroot
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  cd $_gitname
  git submodule init && git submodule update
  cd ..

  rm -rf $_gitname-build
  cp -r $_gitname $_gitname-build
  cd $_gitname-build/

  mkdir build
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release \
    -DNOVA_SIMD=1 -DSC_WII=1 -DSUPERNOVA=1 -DSC_QT=1 -DNATIVE=1 -DSSE42=1

  make
  make DESTDIR=$pkgdir/ install
}

