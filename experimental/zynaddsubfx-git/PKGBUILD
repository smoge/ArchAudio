# Maintainer : SpepS <dreamspepser at yahoo dot it>

_p=zynaddsubfx
pkgname=$_p-git
pkgver=20110710
pkgrel=1
pkgdesc="A powerful realtime, multi-timbral software synthesizer."
arch=('i686' 'x86_64')
url="http://zynaddsubfx.sourceforge.net"
license=('GPL')
depends=('fltk' 'fftw' 'portaudio' 'lash')
makedepends=('git' 'cmake' 'mxml')
provides=("$_p")
conflicts=("$_p")
options=('!emptydirs')
install="$pkgname.install"
source=("http://zynaddsubfx.sourceforge.net/doc/instruments/unsortedzynaddsubfxParameters_20101203.zip"
        "http://zynaddsubfx.sourceforge.net/doc/instruments/banks20090520.zip"
        "http://rekkerd.org/bin/presets/folderol_zynaddsubfx_Collection.zip"
        "$_p.desktop"
        "$_p.svg")
md5sums=('5a89314e4f8abe61f16544364b7cfeec'
         '7d7974e877b818fb562cc870d5886fc5'
         '271ca88e262d3d3378f8d695a7151d1b'
         '5da6735ee59fdfa21f171fdc4d6c80cb'
         '6f7e9c3ce3947088a10c99c46a65431f')

_gitroot="git://$_p.git.sourceforge.net/gitroot/$_p/$_p"
_gitname="$_p"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #

  mkdir build && cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make

  # build external programs
  cd ../ExternalPrograms/Spliter && make
  cd ../Controller &&  sed -i "s|Box\.h|Box\.H|" ControllerUI.fl && make
}

package() {
  cd "$srcdir/$_gitname-build/build"

  make DESTDIR="$pkgdir/" install

  # external programs
  install -Dm644 ../ExternalPrograms/Spliter/spliter \
                 "$pkgdir/usr/bin/spliter"
  install -Dm644 ../ExternalPrograms/Controller/controller \
                 "$pkgdir/usr/bin/controller"

  # spliter doc
  install -Dm644 ../ExternalPrograms/Spliter/readme.txt \
                 "$pkgdir/usr/share/doc/$_p/SPLITER.txt"

  # icon and desktop file
  install -Dm644 "$srcdir/$_p.desktop" \
                 "$pkgdir/usr/share/applications/$_p.desktop"
  install -Dm644 "$srcdir/$_p.svg" \
                 "$pkgdir/usr/share/pixmaps/$_p.svg"

  # banks
  install -d "$pkgdir/usr/share/$_p/banks/Experimental"
  cp -a "$srcdir/banks20090520/"* "$pkgdir/usr/share/$_p/banks"
  cp -a "$srcdir/zynaddsubfxParameters_20101203/"* \
        "$pkgdir/usr/share/$_p/banks/Experimental"
  cp -a "$srcdir/Collection" \
        "$pkgdir/usr/share/$_p/banks"
}
