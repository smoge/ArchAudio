# Maintainer : SpepS <dreamspepser at yahoo dot it>

_p=zynaddsubfx
_parver=20111001
pkgname=$_p-git
pkgver=20111130
pkgrel=1
pkgdesc="A powerful realtime, multi-timbral software synthesizer."
arch=('i686' 'x86_64')
url="http://zynaddsubfx.sourceforge.net"
license=('GPL')
depends=('fltk' 'fftw' 'portaudio' 'lash' 'mxml')
makedepends=('git' 'cmake' 'dssi')
optdepends=('dssi: dssi plugin')
provides=("$_p")
conflicts=("$_p")
options=('!emptydirs')
install="$pkgname.install"
source=("http://zynaddsubfx.sourceforge.net/doc/instruments/unsortedzynaddsubfxParameters_20111001.zip"
        "http://zynaddsubfx.sourceforge.net/doc/instruments/banks20090520.zip"
        "http://rekkerd.org/bin/presets/folderol_zynaddsubfx_Collection.zip"
        "$_p-jack.desktop"
        "$_p-alsa.desktop"
        "$_p.svg")
md5sums=('7d960c52c31320135a2d2a95f37f369e'
         '7d7974e877b818fb562cc870d5886fc5'
         '271ca88e262d3d3378f8d695a7151d1b'
         '137baa3407ca0a9ce3d7f4644723978f'
         '9825fcb4efc641ce1806d58cf1389aa9'
         '6f7e9c3ce3947088a10c99c46a65431f')

_gitroot="git://$_p.git.sourceforge.net/gitroot/$_p/$_p"
_gitname="$_p"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #

  # install dssi plugin in /usr/lib
  sed -i 's/lib64/lib/' src/CMakeLists.txt

  mkdir build && cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make

  # build external programs
  cd ../ExternalPrograms/Spliter && make
  cd ../Controller && make
}

package() {
  cd "$srcdir/$_gitname-build/build"

  make DESTDIR="$pkgdir/" install

  # external programs
  install -Dm644 ../ExternalPrograms/Spliter/spliter \
                 "$pkgdir/usr/bin/spliter"
  install -Dm644 ../ExternalPrograms/Controller/controller \
                 "$pkgdir/usr/bin/controller"

  # spliter doc
  install -Dm644 ../ExternalPrograms/Spliter/readme.txt \
                 "$pkgdir/usr/share/doc/$_p/SPLITER.txt"

  # icon and desktop file
  install -Dm644 "$srcdir/$_p-jack.desktop" \
                 "$pkgdir/usr/share/applications/$_p-jack.desktop"
  install -Dm644 "$srcdir/$_p-alsa.desktop" \
                 "$pkgdir/usr/share/applications/$_p-alsa.desktop"
  install -Dm644 "$srcdir/$_p.svg" \
                 "$pkgdir/usr/share/pixmaps/$_p.svg"

  # banks
  install -d "$pkgdir/usr/share/$_p/banks/Experimental"
  cp -a "$srcdir/banks20090520/"* "$pkgdir/usr/share/$_p/banks"
  cp -a "$srcdir/zynaddsubfxParameters/"* \
        "$pkgdir/usr/share/$_p/banks/Experimental"
  cp -a "$srcdir/Collection" \
        "$pkgdir/usr/share/$_p/banks"
}
