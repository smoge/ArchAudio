# $Id$
# Maintainer: Arch Linux Pro Audio <dev@archaudio.org>
# Contributor: Ray Rashif <schivmeister@gmail.com>
# Contributor: tobias <tobias@archlinux.org>

# ardour, liblrdf and raptor are heavily dependent on each other. Updating of
# one package mostly will require an update of all the other packages. I vote
# for fixed dependencies speaking in terms of versions

pkgname=ardour
pkgver=2.8.4
pkgrel=1
pkgdesc="Professional-grade Digital Audio Workstation"
arch=(i686 x86_64)
license=('GPL')
url="http://ardour.org/"
depends=('liblrdf>=0.4.0' 'liblrdf<0.4.1'
         'libgnomecanvasmm' 'liblo' 'libusb' 'aubio' 'flac'
         'slv2' 'rubberband')
optdepends=('cwiid: Wiimote support')
makedepends=('subversion' 'ladspa' 'scons' 'boost' 'cwiid')
install=$pkgname.install
source=($pkgname.desktop)

build() {
  cd "$srcdir"

  warning "Downloading from VCS as upstream has no direct-download..."
  if [ ! -d $pkgname-svn ]; then
    svn co http://subversion.ardour.org/svn/ardour2/tags/$pkgver $pkgname-svn
  fi
  rm -rf $pkgname-$pkgver
  cp -a $pkgname-svn $pkgname-$pkgver
  msg "Download complete or server timeout"
  msg "Starting build process..."

  # start building ardour now
  cd $pkgname-$pkgver

  sed -i '/-O3/d' SConstruct

  scons $MAKEFLAGS ARCH="$CFLAGS" \
        PREFIX="/usr" \
	FREESOUND=1 \
	WIIMOTE=1 \
        DESTDIR="$pkgdir" || return 1

  scons PREFIX="/usr" \
	FREESOUND=1 \
	WIIMOTE=1 \
        DESTDIR="$pkgdir" install

  # freedesktop.org compatibility
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}
md5sums=('a2cbe522d7fb44fd99edc62a584a0561')
